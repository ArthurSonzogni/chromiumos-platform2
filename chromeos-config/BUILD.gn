# Copyright 2018 The Chromium OS Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/pkg_config.gni")

group("all") {
  deps = [
    ":cros_config",
    ":libcros_config",
  ]
  if (use.test) {
    deps += [
      ":cros_config_json_test",
      ":cros_config_main_json_test",
      ":cros_config_main_test",
      ":cros_config_test",
      ":fake_cros_config_test",
    ]
  }
}

pkg_config("target_defaults") {
  pkg_deps = [ "libchrome-${libbase_ver}" ]
}

shared_library("libcros_config") {
  configs += [ ":target_defaults" ]
  sources = [
    "libcros_config/cros_config.cc",
    "libcros_config/cros_config_fdt.cc",
    "libcros_config/cros_config_impl.cc",
    "libcros_config/cros_config_json.cc",
    "libcros_config/fake_cros_config.cc",
    "libcros_config/identity.cc",
    "libcros_config/identity_arm.cc",
    "libcros_config/identity_x86.cc",
  ]
  libs = [ "-lfdt" ]
}

pkg_config("cros_config_config") {
  pkg_deps = [ "libbrillo-${libbase_ver}" ]
}

executable("cros_config") {
  configs += [
    ":target_defaults",
    ":cros_config_config",
  ]
  sources = [
    "cros_config_main.cc",
  ]
  deps = [
    ":libcros_config",
  ]
}

if (use.test) {
  executable("cros_config_test") {
    configs += [
      "//common-mk:test",
      ":target_defaults",
    ]
    sources = [
      "libcros_config/cros_config_test.cc",
    ]
    include_dirs = [ "libcros_config" ]
    deps = [
      ":libcros_config",
    ]
  }

  executable("cros_config_json_test") {
    configs += [
      "//common-mk:test",
      ":target_defaults",
    ]
    sources = [
      "libcros_config/cros_config_test.cc",
    ]
    include_dirs = [ "libcros_config" ]
    defines = [ "USE_JSON" ]
    deps = [
      ":libcros_config",
    ]
  }

  executable("cros_config_main_test") {
    configs += [
      "//common-mk:test",
      ":target_defaults",
    ]
    sources = [
      "cros_config_main_test.cc",
    ]
    deps = [
      ":cros_config",
    ]
  }

  executable("cros_config_main_json_test") {
    configs += [
      "//common-mk:test",
      ":target_defaults",
    ]
    sources = [
      "cros_config_main_test.cc",
    ]
    defines = [ "USE_JSON" ]
    deps = [
      ":cros_config",
    ]
  }

  executable("fake_cros_config_test") {
    configs += [
      "//common-mk:test",
      ":target_defaults",
    ]
    sources = [
      "libcros_config/fake_cros_config_test.cc",
    ]
    include_dirs = [ "libcros_config" ]
    deps = [
      ":libcros_config",
    ]
  }
}
