# Copyright 2024 The ChromiumOS Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/deps.gni")
import("//common-mk/pkg_config.gni")

group("all") {
  deps = [
    ":intel_pmt_plugin",
    ":pmt_tool",
  ]
  if (use.test) {
    deps += [ ":pmt_tool_tests" ]
  }
}

pkg_config("target_defaults_pkg_deps") {
  pkg_deps = [
    "libbrillo",
    "libchrome",
    "libpmt",
    "protobuf",
  ]
}

executable("pmt_tool") {
  sources = [
    "main.cc",
    "pmt_tool.cc",
    "utils.cc",
  ]
  configs += [ ":target_defaults_pkg_deps" ]
  install_path = "bin"
}

if (use.test) {
  executable("pmt_tool_tests") {
    sources = [
      "pmt_tool.cc",
      "pmt_tool_test.cc",
      "utils.cc",
    ]
    configs += [
      "//common-mk:test",
      ":target_defaults_pkg_deps",
    ]
    run_test = true
    deps = [
      ":pmt_tool",
      "//common-mk/testrunner",
    ]
  }
}

executable("intel_pmt_plugin") {
  output_name = "intel_pmt.plugin"
  sources = [ "netdata-plugin/intel_pmt_plugin.cc" ]
  configs += [ ":target_defaults_pkg_deps" ]
  install_path = "/usr/local/etc/netdata/custom-plugins.d"
}
