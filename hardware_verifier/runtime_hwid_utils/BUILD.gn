# Copyright 2025 The ChromiumOS Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/pkg_config.gni")

group("all") {
  deps = [
    ":install_headers",
    ":libruntimehwid_utils",
    ":libruntimehwid_utils-pc",
    ":runtime_hwid_tool",
  ]
  if (use.test) {
    deps += [ ":unittest_runner" ]
  }
}

pkg_config("target_defaults") {
  pkg_deps = [
    "libbrillo",
    "libchrome",
    "libcrossystem",
  ]
}

shared_library("libruntimehwid_utils") {
  output_name = "libruntimehwid_utils"
  sources = [ "runtime_hwid_utils_impl.cc" ]
  configs += [ ":target_defaults" ]
  install_path = "lib"
}

# Install headers to /usr/include/chromeos/.
# Args:
#   sources: A list of headers to be installed.
#   subdir: The subfolder name under platform2/. Headers will be installed to
#     /usr/include/chromeos/${subdir}/.
template("install_chromeos_headers") {
  assert(defined(invoker.sources),
         "Need sources in $target_name listing the header files to install.")
  assert(
      defined(invoker.subdir),
      "Need subdir in $target_name specifying the folder name under platform2/.")

  preprocessing_target_name = "pre_" + target_name

  action_foreach(preprocessing_target_name) {
    forward_variables_from(invoker,
                           [
                             "sources",
                             "subdir",
                           ])
    outputs =
        [ "${target_gen_dir}/include/chromeos/${subdir}/{{source_file_part}}" ]

    script = "/bin/sh"
    args = [
      "-c",
      "sed '/.pb.h/! s:${subdir}/:chromeos/${subdir}/:g' " + "{{source}}" +
          "> ${target_gen_dir}/include/chromeos/${subdir}/{{source_file_part}}",
    ]
  }

  install_config(target_name) {
    forward_variables_from(invoker, [ "subdir" ])
    sources = get_target_outputs(":${preprocessing_target_name}")
    install_path = "/usr/include/chromeos/${subdir}"
    deps = [ ":${preprocessing_target_name}" ]
  }
}

install_chromeos_headers("install_headers") {
  sources = [
    "runtime_hwid_utils.h",
    "runtime_hwid_utils_impl.h",
    "runtime_hwid_utils_mock.h",
  ]
  subdir = "hardware_verifier/runtime_hwid_utils"
}

generate_pkg_config("libruntimehwid_utils-pc") {
  name = "libruntimehwid_utils"
  description = "Runtime HWID Utility library"
  version = getenv("PV")
  libs = [ "-lruntimehwid_utils" ]
  requires = [
    "libbrillo",
    "libchrome",
    "libcrossystem",
  ]
  install = true
}

executable("runtime_hwid_tool") {
  sources = [ "runtime_hwid_tool.cc" ]
  configs += [ ":target_defaults" ]
  install_path = "bin"
  deps = [ ":libruntimehwid_utils" ]
}

if (use.test) {
  executable("unittest_runner") {
    sources = [ "runtime_hwid_utils_impl_test.cc" ]
    configs += [
      "//common-mk:test",
      ":target_defaults",
    ]
    run_test = true
    pkg_deps = [ "libcrossystem-test" ]
    deps = [
      ":libruntimehwid_utils",
      "//common-mk/testrunner",
    ]
  }
}
