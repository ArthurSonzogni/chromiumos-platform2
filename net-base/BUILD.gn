# Copyright 2023 The ChromiumOS Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/pkg_config.gni")

group("all") {
  deps = [
    ":libnet-base",
    ":libnet-base_pc",
  ]
  if (use.fuzzer) {
    deps += [
      ":http_url_fuzzer",
      ":netlink_attribute_list_fuzzer",
      ":rtnl_handler_fuzzer",
    ]
  }
  if (use.test) {
    deps += [ ":libnet-base-testrunner" ]
  }
}

pkg_config("target_defaults") {
  cflags = [
    "-Wconversion",
    "-Wextra",
    "-Werror",
    "-Wno-unused-parameter",  # needed for libchrome
  ]
  pkg_deps = [
    "libcares",
    "libchrome",
  ]
}

shared_library("libnet-base") {
  sources = [
    "ares_interface.cc",
    "attribute_list.cc",
    "byte_utils.cc",
    "control_netlink_attribute.cc",
    "dns_client.cc",
    "http_url.cc",
    "ip_address.cc",
    "ip_address_utils.cc",
    "ipv4_address.cc",
    "ipv6_address.cc",
    "mac_address.cc",
    "netlink_attribute.cc",
    "netlink_message.cc",
    "netlink_packet.cc",
    "netlink_sock_diag.cc",
    "netlink_socket.cc",
    "network_config.cc",
    "network_priority.cc",
    "rtnl_handler.cc",
    "rtnl_listener.cc",
    "rtnl_message.cc",
    "socket.cc",
  ]
  configs += [ ":target_defaults" ]
  install_path = "lib"
}

generate_pkg_config("libnet-base_pc") {
  name = "libnet-base"
  description = "Network primitive library"
  version = getenv("PV")
  requires = [ "libchrome" ]
  libs = [ "-lnet-base" ]
  install = true
}

if (use.fuzzer) {
  executable("http_url_fuzzer") {
    sources = [ "http_url_fuzzer.cc" ]
    configs += [
      "//common-mk/common_fuzzer",
      ":target_defaults",
    ]
    deps = [ ":libnet-base" ]
  }
  executable("netlink_attribute_list_fuzzer") {
    sources = [ "attribute_list_fuzzer.cc" ]
    configs += [
      "//common-mk/common_fuzzer",
      ":target_defaults",
    ]
    deps = [ ":libnet-base" ]
  }
  executable("rtnl_handler_fuzzer") {
    sources = [ "rtnl_handler_fuzzer.cc" ]
    configs += [
      "//common-mk/common_fuzzer",
      ":target_defaults",
    ]
    deps = [ ":libnet-base" ]
  }
}

if (use.test) {
  source_set("net_base_test_support") {
    sources = [
      "mock_netlink_socket.cc",
      "mock_rtnl_handler.cc",
      "mock_socket.cc",
    ]
    configs += [
      "//common-mk:test",
      ":target_defaults",
    ]
    pkg_deps = [
      "libnet-base",
      "re2",
    ]
  }

  executable("libnet-base-testrunner") {
    run_test = true
    sources = [
      "attribute_list_test.cc",
      "byte_utils_test.cc",
      "dns_client_test.cc",
      "http_url_test.cc",
      "ip_address_test.cc",
      "ipv4_address_test.cc",
      "ipv6_address_test.cc",
      "mac_address_test.cc",
      "netlink_attribute_test.cc",
      "netlink_packet_test.cc",
      "netlink_socket_test.cc",
      "rtnl_handler_test.cc",
      "rtnl_listener_test.cc",
      "rtnl_message_test.cc",
      "socket_test.cc",
    ]
    configs += [
      "//common-mk:test",
      ":target_defaults",
    ]
    deps = [
      ":libnet-base",
      ":net_base_test_support",
      "//common-mk/testrunner",
    ]
    pkg_deps = [ "libchrome-test" ]
  }
}
