# Copyright 2023 The ChromiumOS Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/pkg_config.gni")

group("all") {
  deps = [
    ":libnet-base",
    ":libnet-base_pc",
  ]
  if (use.fuzzer) {
    deps += [ ":rtnl_handler_fuzzer" ]
  }
  if (use.test) {
    deps += [ ":libnet-base-testrunner" ]
  }
}

pkg_config("target_defaults") {
  cflags = [
    "-Wconversion",
    "-Wextra",
    "-Werror",
    "-Wno-unused-parameter",  # needed for libchrome
  ]
  pkg_deps = [
    "libcares",
    "libchrome",
  ]
}

shared_library("libnet-base") {
  sources = [
    "ares_interface.cc",
    "byte_utils.cc",
    "dns_client.cc",
    "ip_address.cc",
    "ip_address_utils.cc",
    "ipv4_address.cc",
    "ipv6_address.cc",
    "mac_address.cc",
    "netlink_sock_diag.cc",
    "rtnl_handler.cc",
    "rtnl_listener.cc",
    "rtnl_message.cc",
    "socket.cc",
  ]
  configs += [ ":target_defaults" ]
  install_path = "lib"
}

generate_pkg_config("libnet-base_pc") {
  name = "libnet-base"
  description = "Network primitive library"
  version = getenv("PV")
  requires = [ "libchrome" ]
  libs = [ "-lnet-base" ]
  install = true
}

if (use.fuzzer) {
  executable("rtnl_handler_fuzzer") {
    sources = [ "rtnl_handler_fuzzer.cc" ]
    configs += [
      "//common-mk/common_fuzzer",
      ":target_defaults",
    ]
    deps = [ ":libnet-base" ]
  }
}

if (use.test) {
  source_set("net_base_test_support") {
    sources = [
      "mock_rtnl_handler.cc",
      "mock_socket.cc",
    ]
    configs += [
      "//common-mk:test",
      ":target_defaults",
    ]
    pkg_deps = [
      "libnet-base",
      "re2",
    ]
  }

  executable("libnet-base-testrunner") {
    run_test = true
    sources = [
      "byte_utils_test.cc",
      "dns_client_test.cc",
      "ip_address_test.cc",
      "ipv4_address_test.cc",
      "ipv6_address_test.cc",
      "mac_address_test.cc",
      "rtnl_handler_test.cc",
      "rtnl_listener_test.cc",
      "rtnl_message_test.cc",
      "socket_test.cc",
    ]
    configs += [
      "//common-mk:test",
      ":target_defaults",
    ]
    deps = [
      ":libnet-base",
      ":net_base_test_support",
      "//common-mk/testrunner",
    ]
    pkg_deps = [ "libchrome-test" ]
  }
}
