# Copyright 2018 The Chromium OS Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/pkg_config.gni")

group("all") {
  deps = [
    ":clobber-state",
    ":net_poll_tool",
    ":static_node_tool",
  ]
  if (use.test) {
    deps += [ ":clobber_state_unittest" ]
  }
}

pkg_config("target_defaults") {
  pkg_deps = [
    "libbrillo-${libbase_ver}",
    "libchrome-${libbase_ver}",
    "vboot_host"
  ]
}

static_library("libcrossystem") {
  configs += [ ":target_defaults" ]
  sources = [
    "crossystem.cc",
    "crossystem_fake.cc",
    "crossystem_impl.cc",
  ]
}

executable("clobber-state") {
  configs += [ ":target_defaults" ]
  sources = [
    "clobber_state.cc",
    "clobber_state_main.cc",
  ]
}

executable("net_poll_tool") {
  configs += [ ":target_defaults" ]
  sources = [
    "net_poll_tool.cc",
  ]
}

executable("static_node_tool") {
  configs += [ ":target_defaults" ]
  sources = [
    "static_node_tool.cc",
  ]
}
if (use.test) {
  pkg_config("test_config") {
    pkg_deps = [ "libchrome-test-${libbase_ver}" ]
  }

  executable("clobber_state_unittest") {
    configs += [
      "//common-mk:test",
      ":target_defaults",
      ":test_config",
    ]
    sources = [
      "clobber_state.cc",
      "clobber_state_unittest.cc",
    ]
    deps = [
      "//common-mk/testrunner",
    ]
  }
}
