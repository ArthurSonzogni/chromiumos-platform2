# Copyright 2020 The Chromium OS Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/pkg_config.gni")

group("all") {
  deps = [ ":typecd" ]
  if (use.test) {
    deps += [ ":typecd_testrunner" ]
  }
}

pkg_config("target_defaults") {
  pkg_deps = [
    "libbrillo",
    "libchrome-${libbase_ver}",
    "libudev",
  ]
}

executable("typecd") {
  configs += [ ":target_defaults" ]
  deps = [ ":typecd_common" ]
  sources = [
    "daemon.cc",
    "main.cc",
  ]
}

static_library("typecd_common") {
  all_dependent_configs = [ ":target_defaults" ]
  sources = [
    "partner.cc",
    "udev_monitor.cc",
  ]
}

if (use.test) {
  executable("typecd_testrunner") {
    configs += [ "//common-mk:test" ]
    deps = [
      ":typecd_common",
      "//common-mk/testrunner:testrunner",
    ]
    sources = [ "partner_test.cc" ]
  }
}
