# Copyright 2024 The ChromiumOS Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/ebpf_gen.gni")
import("//common-mk/pkg_config.gni")

group("all") {
  deps = [
    ":bpf-fdmon",
    ":bpf-memmon",
  ]
}

pkg_config("target_defaults") {
  # NOSORT
  pkg_deps = [ "libbpf" ]
}

generate_vmlinux("vmlinux_h") {
  vmlinux_out_dir = "include/mons/vmlinux"
  btf_out_dir = "bpf"
}

generate_ebpf_skeletons("bpf_memmon") {
  sources = [ "memmon.bpf.c" ]
  bpf_skeleton_out_dir = "include/mons/bpf_skeletons"
  btf_inout_dir = "bpf"
  deps = [ ":vmlinux_h" ]
}

generate_ebpf_skeletons("bpf_fdmon") {
  sources = [ "fdmon.bpf.c" ]
  bpf_skeleton_out_dir = "include/mons/bpf_skeletons"
  btf_inout_dir = "bpf"
  deps = [ ":vmlinux_h" ]
}

executable("bpf-memmon") {
  sources = [
    "libmon.cc",
    "memmon.cc",
  ]
  configs += [ ":target_defaults" ]
  libs = [ "blazesym_c" ]
  deps = [ ":bpf_memmon" ]
  install_path = "bin"
}

executable("bpf-fdmon") {
  sources = [
    "fdmon.cc",
    "libmon.cc",
  ]
  configs += [ ":target_defaults" ]
  libs = [ "blazesym_c" ]
  deps = [ ":bpf_fdmon" ]
  install_path = "bin"
}
