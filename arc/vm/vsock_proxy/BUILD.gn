# Copyright 2018 The Chromium OS Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/pkg_config.gni")
import("//common-mk/proto_library.gni")

group("all") {
  deps = [
    ":server_proxy",
  ]
  if (use.test) {
    deps += [ ":vsock_proxy_test" ]
  }
}

pkg_config("target_defaults") {
  pkg_deps = [
    "libchrome-${libbase_ver}",
    "libbrillo-${libbase_ver}",
    "protobuf",
  ]
}

proto_library("message") {
  proto_in_dir = "."
  proto_out_dir = "include/arc/vm/vsock_proxy"
  sources = [
    "${proto_in_dir}/message.proto",
  ]
}

static_library("vsock_proxy_lib") {
  sources = [
    "file_descriptor_util.cc",
    "file_stream.cc",
    "fuse_mount.cc",
    "proxy_file_system.cc",
    "server_proxy.cc",
    "socket_stream.cc",
    "vsock_proxy.cc",
    "vsock_stream.cc",
  ]
  configs += [ ":target_defaults" ]
  pkg_deps = [ "fuse" ]
  defines = [ "FUSE_USE_VERSION=26" ]
  deps = [
    ":message",
  ]
}

executable("server_proxy") {
  sources = [
    "server_proxy_main.cc",
  ]
  configs += [ ":target_defaults" ]
  deps = [
    ":vsock_proxy_lib",
  ]
}

if (use.test) {
  executable("vsock_proxy_test") {
    sources = [
      "file_descriptor_util_test.cc",
      "file_stream_test.cc",
      "proxy_file_system_test.cc",
      "socket_stream_test.cc",
      "vsock_proxy_test.cc",
      "vsock_stream_test.cc",
    ]
    configs += [
      "//common-mk:test",
      ":target_defaults",
    ]
    pkg_deps = [
      "libbrillo-test-${libbase_ver}",
      "libchrome-test-${libbase_ver}",
    ]
    deps = [
      ":vsock_proxy_lib",
      "//common-mk/testrunner",
    ]
  }
}
