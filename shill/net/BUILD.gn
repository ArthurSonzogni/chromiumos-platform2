# Copyright 2020 The ChromiumOS Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/deps.gni")
import("//common-mk/pkg_config.gni")

group("all") {
  deps = [
    ":install_headers",
    ":libshill-net",
  ]
  if (use.fuzzer) {
    deps += [ ":netlink_attribute_list_fuzzer" ]
  }
  if (use.test) {
    deps += [ ":shill_net_test" ]
  }
}

install_config("install_headers") {
  sources = [
    "attribute_list.h",
    "control_netlink_attribute.h",
    "generic_netlink_message.h",
    "ieee80211.h",
    "mock_netlink_manager.h",
    "mock_process_manager.h",
    "netlink_attribute.h",
    "netlink_manager.h",
    "netlink_message.h",
    "netlink_packet.h",
    "process_manager.h",
    "shill_export.h",
  ]
  install_path = "/usr/include/shill/net"
}

pkg_config("target_defaults") {
  cflags_cc = [
    "-fno-strict-aliasing",
    "-Woverloaded-virtual",
    "-Wno-missing-field-initializers",  # for LAZY_INSTANCE_INITIALIZER
  ]
  cflags = [
    "-Wextra",
    "-Werror",
    "-Wno-unused-parameter",
  ]
  pkg_deps = [
    "libbrillo",
    "libchrome",
    "libminijail",
    "libnet-base",
  ]
}

write_deps("libshill-net_deps") {
  pkg_deps = []
  target = "libshill-net"
}

shared_library("libshill-net") {
  sources = [
    "attribute_list.cc",
    "control_netlink_attribute.cc",
    "generic_netlink_message.cc",
    "ieee80211.cc",
    "netlink_attribute.cc",
    "netlink_manager.cc",
    "netlink_message.cc",
    "netlink_packet.cc",
    "process_manager.cc",
  ]
  configs += [ ":target_defaults" ]
  deps = [ ":${target_name}_deps" ]
  install_path = "lib"
}

if (use.fuzzer) {
  pkg_config("fuzzed_data_config") {
    pkg_deps = [ "libchrome-test" ]
  }
  executable("netlink_attribute_list_fuzzer") {
    sources = [ "attribute_list_fuzzer.cc" ]
    configs += [
      "//common-mk/common_fuzzer",
      ":target_defaults",
    ]
    deps = [ ":libshill-net" ]
  }
}

if (use.test) {
  source_set("net_test_support") {
    sources = [
      "mock_netlink_manager.cc",
      "mock_process_manager.cc",
    ]
    configs += [
      "//common-mk:test",
      ":target_defaults",
    ]
    pkg_deps = [ "libshill-net" ]
    deps = [ "//net-base:net_base_test_support" ]
  }

  pkg_config("shill_net_test_config") {
    pkg_deps = [
      "libchrome-test",
      "re2",
    ]
  }

  executable("shill_net_test") {
    run_test = true
    sources = [
      "attribute_list_test.cc",
      "netlink_attribute_test.cc",
      "netlink_packet_test.cc",
      "process_manager_test.cc",
    ]
    configs += [
      "//common-mk:test",
      ":target_defaults",
      ":shill_net_test_config",
    ]
    deps = [
      ":libshill-net",
      ":net_test_support",
      "//common-mk/testrunner",
      "//net-base:net_base_test_support",
    ]
  }
}
