# Copyright 2018 The Chromium OS Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/pkg_config.gni")

group("all") {
  deps = [
    ":u2fd",
    ":g2ftool",
  ]
  if (use.test) {
    deps += [
      ":g2f_client_test",
    ]
  }
}

pkg_config("target_defaults") {
  pkg_deps = [
    "libbrillo-${libbase_ver}",
    "libchrome-${libbase_ver}",
    "libpower_manager-client",
    "libtrunks",
    "system_api",
  ]
}

executable("u2fd") {
  configs += [ ":target_defaults" ]
  sources = [
    "main.cc",
    "tpm_vendor_cmd.cc",
    "u2fhid.cc",
    "uhid_device.cc",
  ]
  libs = [ "policy-${libbase_ver}" ]
  deps = [
    "//common-mk/external_dependencies:policy-protos",
  ]
}

executable("g2ftool") {
  configs += [ ":target_defaults" ]
  sources = [
    "g2f_tools/g2ftool.cc",
  ]
  deps = [
    ":libg2fclient",
  ]
}

static_library("libg2fclient") {
  configs += [ ":target_defaults" ]
  sources = [
    "g2f_tools/g2f_client.cc",
  ]
  libs = [ "hidapi-hidraw" ]
}

if (use.test) {
   pkg_config("g2f_client_test_config") {
    pkg_deps = [
      "libbrillo-${libbase_ver}",
      "libchrome-${libbase_ver}",
      "libpower_manager-client",
      "libtrunks",
      "system_api",
      "libchrome-test-${libbase_ver}",
    ]
  }

  executable("g2f_client_test") {
    configs += [
      "//common-mk:test",
      ":g2f_client_test_config",
    ]
    sources = [
      "g2f_tools/g2f_client.cc",
      "g2f_tools/g2f_client_test.cc",
    ]
    deps = [
      "//common-mk/testrunner",
    ]
  }
}
