# Copyright 2024 The ChromiumOS Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/mojom_bindings_generator.gni")
import("//common-mk/pkg_config.gni")
import("//common-mk/proto_library.gni")

group("all") {
  if (use.feature_management) {
    deps = [
      ":install_dbus_config",
      ":install_init",
      ":install_minijail_conf",
      ":install_policy",
      ":install_service_manager_policy_config",
      ":libdmabufheap",
      ":odml_console",
      ":odmld",
    ]
    if (use.test) {
      deps += [
        ":coral_testrunner",
        ":odml_testrunner",
      ]
    }
  }
}

pkg_config("target_defaults") {
  pkg_deps = [
    "absl",
    "libbrillo",
    "libchrome",
    "libdlcservice-client",
    "libmetrics",
    "mojo_service_manager",
    "protobuf-lite",
    "system_api",
  ]
}

source_set("libdmabufheap_source_set") {
  sources = [
    "dmabufheap/buffer_allocator.cc",
    "dmabufheap/buffer_allocator_wrapper.cc",
  ]

  # Use -fPIC so this code can be linked into a shared library.
  configs += [ "//common-mk:pic" ]
}

shared_library("libdmabufheap") {
  deps = [ ":libdmabufheap_source_set" ]
  install_path = "lib"
}

generate_mojom_bindings("mojo_bindings") {
  mojo_root = "${platform2_root}"
  if (use.ml_benchmark_drivers) {
    use_pic = true
  }
  sources = [
    "mojom/coral_service.mojom",
    "mojom/embedding_model.mojom",
    "mojom/file.mojom",
    "mojom/file_path.mojom",
    "mojom/mantis_processor.mojom",
    "mojom/mantis_service.mojom",
    "mojom/on_device_model.mojom",
    "mojom/on_device_model_service.mojom",
    "mojom/read_only_file.mojom",
    "mojom/url.mojom",
    "mojom/uuid.mojom",
  ]
  mojo_extra_args = [
    "--typemap",
    rebase_path("mojom/type_mappings_internal.json"),
  ]
  enabled_features = [
    "file_path_is_string",
    "is_chromeos_ash",
  ]
}

source_set("mojo_bindings_lib") {
  sources = [
    "mojom/adaptation_assets_mojom_traits.h",
    "mojom/file_mojom_traits.cc",
    "mojom/file_mojom_traits.h",
    "mojom/file_path_mojom_traits.cc",
    "mojom/file_path_mojom_traits.h",
    "mojom/model_assets_mojom_traits.h",
    "mojom/read_only_file_mojom_traits.cc",
    "mojom/read_only_file_mojom_traits.h",
    "mojom/uuid_mojom_traits.cc",
    "mojom/uuid_mojom_traits.h",
  ]
  public_deps = [ ":mojo_bindings" ]
  configs += [ ":target_defaults" ]
}

source_set("coral") {
  sources = [
    "coral/clustering/agglomerative_clustering.cc",
    "coral/clustering/clustering_factory.cc",
    "coral/clustering/engine.cc",
    "coral/embedding/engine.cc",
    "coral/service.cc",
    "coral/title_generation/engine.cc",
    "coral/title_generation/simple_session.cc",
  ]
  public_deps = [ ":mojo_bindings_lib" ]
  configs += [ ":target_defaults" ]
}

source_set("mantis") {
  sources = [
    "mantis/processor.cc",
    "mantis/service.cc",
  ]
  public_deps = [ ":mojo_bindings_lib" ]
  configs += [ ":target_defaults" ]
}

source_set("odml") {
  sources = [
    "embedding_model/embedding_model_service.cc",
    "on_device_model/ml/chrome_ml.cc",
    "on_device_model/ml/gpu_blocklist.cc",
    "on_device_model/ml/on_device_model_executor.cc",
    "on_device_model/ml/on_device_model_internal.cc",
    "on_device_model/ml/session_accessor.cc",
    "on_device_model/ml/ts_model.cc",
    "on_device_model/ml/utils.cc",
    "on_device_model/on_device_model_service.cc",
    "on_device_model/platform_model_loader_chromeos.cc",
    "on_device_model/public/cpp/model_assets.cc",
    "utils/dlc_client_helper.cc",
    "utils/odml_shim_loader_impl.cc",
  ]
  public_deps = [
    ":mojo_bindings_lib",
    "//ml_core/dlc:dlc_client",
  ]
  configs += [ ":target_defaults" ]
}

executable("odml_console") {
  install_path = "/usr/local/bin"
  sources = [ "odml_console.cc" ]
  configs += [ ":target_defaults" ]
  deps = [ ":odml" ]
}

executable("odmld") {
  install_path = "bin"
  sources = [ "odmld.cc" ]
  configs += [ ":target_defaults" ]
  deps = [
    ":coral",
    ":mantis",
    ":odml",
  ]
}

# Install the policy for mojo service manager.
install_config("install_service_manager_policy_config") {
  sources = [ "odmld.jsonc" ]
  install_path = "/etc/mojo/service_manager/policy"
}

install_config("install_minijail_conf") {
  sources = [ "minijail/odmld.conf" ]
  install_path = "minijail_conf"
}

install_config("install_init") {
  sources = [ "init/odmld.conf" ]
  install_path = "upstart"
}

install_config("install_policy") {
  _arch = getenv("ARCH")
  sources = [ "minijail/odmld-seccomp-${_arch}.policy" ]
  install_path = "seccomp_policy"
  outputs = [ "odmld-seccomp.policy" ]
}

install_config("install_dbus_config") {
  sources = [ "dbus/org.chromium.Odml.conf" ]
  install_path = "dbus_system_d"
}

if (use.test) {
  pkg_config("test_config") {
    pkg_deps = [ "libchrome-test" ]
  }

  executable("odml_testrunner") {
    sources = [
      "embedding_model/embedding_model_service_test.cc",
      "on_device_model/fake/fake_chrome_ml_api.cc",
      "on_device_model/on_device_model_fake.cc",
      "on_device_model/on_device_model_service_test.cc",
      "on_device_model/public/cpp/test_support/test_response_holder.cc",
    ]
    configs += [
      "//common-mk:test",
      ":target_defaults",
      ":test_config",
    ]
    run_test = true
    deps = [
      ":odml",
      "//common-mk/testrunner",
    ]
  }

  executable("coral_testrunner") {
    sources = [
      "coral/clustering/agglomerative_clustering_test.cc",
      "coral/clustering/engine_test.cc",
      "coral/embedding/engine_test.cc",
      "coral/service_test.cc",
      "coral/title_generation/engine_test.cc",
      "on_device_model/fake/fake_chrome_ml_api.cc",
      "on_device_model/on_device_model_fake.cc",
    ]
    configs += [
      "//common-mk:test",
      ":target_defaults",
      ":test_config",
    ]
    run_test = true
    deps = [
      ":coral",
      ":odml",
      "//common-mk/testrunner",
    ]
  }
}
