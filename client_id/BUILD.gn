# Copyright 2022 The Chromium OS Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/pkg_config.gni")

group("all") {
  deps = [
    ":client_id",
    ":libclient_id",
  ]
  if (use.test) {
    deps += [ ":client_id_test" ]
  }
}

pkg_config("target_defaults") {
  pkg_deps = [
    "libbrillo",
    "libchrome",
  ]
}

static_library("libclient_id") {
  sources = [ "client_id.cc" ]
  configs += [ ":target_defaults" ]
}

executable("client_id") {
  sources = [ "main.cc" ]
  configs += [ ":target_defaults" ]
  deps = [ ":libclient_id" ]
}

if (use.test) {
  pkg_config("client_id_test_config") {
    pkg_deps = [ "libchrome-test" ]
  }

  executable("client_id_test") {
    sources = [ "client_id_test.cc" ]
    configs += [
      "//common-mk:test",
      ":client_id_test_config",
      ":target_defaults",
    ]
    deps = [
      ":libclient_id",
      "//common-mk/testrunner",
    ]
  }
}
