# Copyright 2023 The ChromiumOS Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/pkg_config.gni")
import("//common-mk/proto_library.gni")

group("all") {
  deps = [
    ":install_headers",
    ":install_pkgconfig",
    ":install_proto_header",
    ":libenterprise_rollback_metrics",
  ]
  if (use.test || use.fuzzer) {
    deps += [
      ":install_headers-test",
      ":install_pkgconfig-test",
      ":libenterprise_rollback_metrics-test",
    ]
  }
  if (use.test) {
    deps += [ ":enterprise_rollback_metrics_tests" ]
  }
}

pkg_config("target_defaults") {
  pkg_deps = [
    "libmetrics",
    "libstructuredmetrics",
  ]
  if (use.fuzzer) {
    pkg_deps += [ "protobuf" ]
  } else {
    pkg_deps += [ "protobuf-lite" ]
  }
}

source_set("metrics_uma") {
  sources = [ "metrics_uma.cc" ]
  all_dependent_configs = [ ":target_defaults" ]
}

proto_library("enterprise_rollback_metrics_proto") {
  proto_in_dir = "."
  proto_out_dir = "include/oobe_config/metrics"
  sources = [ "enterprise_rollback_metrics_data.proto" ]

  # PIC is required for shared libraries and their dependencies.
  use_pic = true
  standalone = true
}

source_set("enterprise_rollback_metrics_library_common_sources") {
  sources = [
    "enterprise_rollback_metrics_handler.cc",
    "enterprise_rollback_metrics_recorder.cc",
    "enterprise_rollback_metrics_tracking.cc",
  ]
  deps = [ ":enterprise_rollback_metrics_proto" ]
  all_dependent_configs = [ ":target_defaults" ]

  # PIC is required for shared libraries and their dependencies.
  configs += [ "//common-mk:pic" ]
}

shared_library("libenterprise_rollback_metrics") {
  configs += [ ":target_defaults" ]
  deps = [
    ":enterprise_rollback_metrics_library_common_sources",
    "//oobe_config/filesystem:filesystem",
  ]
  install_path = "lib"
}

install_config("install_pkgconfig") {
  sources = [ "libenterprise_rollback_metrics.pc" ]
  install_path = "${libdir}/pkgconfig"
}

install_config("install_headers") {
  sources = [
    "enterprise_rollback_metrics_handler.h",
    "enterprise_rollback_metrics_tracking.h",
  ]
  install_path = "/usr/include/oobe_config/metrics"
}

install_config("install_proto_header") {
  sources = [ "${root_gen_dir}/include/oobe_config/metrics/enterprise_rollback_metrics_data.pb.h" ]
  install_path = "/usr/include/oobe_config/metrics/"
  deps = [ ":enterprise_rollback_metrics_proto" ]
}

if (use.test || use.fuzzer) {
  install_config("install_headers-test") {
    sources = [ "enterprise_rollback_metrics_handler_for_testing.h" ]
    install_path = "/usr/include/oobe_config/metrics"
  }

  install_config("install_pkgconfig-test") {
    sources = [ "libenterprise_rollback_metrics-test.pc" ]
    install_path = "${libdir}/pkgconfig"
  }

  source_set("enterprise_rollback_metrics_library_test_sources") {
    sources = [ "enterprise_rollback_metrics_handler_for_testing.cc" ]
    configs += [ "//common-mk:test" ]
    deps = [ ":enterprise_rollback_metrics_library_common_sources" ]

    # PIC is required for shared libraries and their dependencies.
    configs += [ "//common-mk:pic" ]
  }

  shared_library("libenterprise_rollback_metrics-test") {
    configs += [
      "//common-mk:test",
      ":target_defaults",
    ]
    deps = [
      ":enterprise_rollback_metrics_library_test_sources",
      "//oobe_config/filesystem:filesystem_for_testing",
    ]
    install_path = "lib"
  }
}

if (use.test) {
  executable("enterprise_rollback_metrics_tests") {
    sources = [
      "enterprise_rollback_metrics_handler_test.cc",
      "enterprise_rollback_metrics_recorder_test.cc",
      "enterprise_rollback_metrics_tracking_test.cc",
    ]
    configs += [
      "//common-mk:test",
      ":target_defaults",
    ]
    run_test = true
    deps = [
      ":enterprise_rollback_metrics_library_test_sources",
      "//common-mk/testrunner",
      "//oobe_config/filesystem:filesystem_for_testing",
    ]
  }
}
