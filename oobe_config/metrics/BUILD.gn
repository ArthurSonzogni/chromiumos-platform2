# Copyright 2023 The ChromiumOS Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/deps.gni")
import("//common-mk/pkg_config.gni")
import("//common-mk/proto_library.gni")

group("all") {
  deps = [
    ":install_headers",
    ":install_headers-test",
    ":install_proto_header",
    ":libenterprise_rollback_metrics",
    ":libenterprise_rollback_metrics-test",
  ]
  if (use.test) {
    deps += [ ":enterprise_rollback_metrics_tests" ]
  }
}

pkg_config("target_defaults") {
  pkg_deps = [
    "libmetrics",
    "libstructuredmetrics",
  ]
  if (use.fuzzer) {
    pkg_deps += [ "protobuf" ]
  } else {
    pkg_deps += [ "protobuf-lite" ]
  }
}

proto_library("enterprise_rollback_metrics_proto") {
  proto_in_dir = "."
  proto_out_dir = "include/oobe_config/metrics"
  sources = [ "enterprise_rollback_metrics_data.proto" ]

  # PIC is required for shared libraries and their dependencies.
  use_pic = true
  standalone = true
}

source_set("enterprise_rollback_metrics_library_common_sources") {
  sources = [
    "enterprise_rollback_metrics_handler.cc",
    "enterprise_rollback_metrics_recorder.cc",
    "enterprise_rollback_metrics_tracking.cc",
  ]
  deps = [ ":enterprise_rollback_metrics_proto" ]
  all_dependent_configs = [ ":target_defaults" ]

  # PIC is required for shared libraries and their dependencies.
  configs += [ "//common-mk:pic" ]
}

generate_pkg_config("libenterprise_rollback_metrics_pc") {
  name = "libenterprise_rollback_metrics"
  output_name = "libenterprise_rollback_metrics"
  description = "Enterprise Rollback metrics library"
  version = "0.0"

  libs = [ "-lenterprise_rollback_metrics" ]
  install = true
}

shared_library("libenterprise_rollback_metrics") {
  configs += [ ":target_defaults" ]
  deps = [
    ":enterprise_rollback_metrics_library_common_sources",
    ":libenterprise_rollback_metrics_pc",
    "//oobe_config/filesystem:filesystem",
  ]
  install_path = "lib"
}

install_config("install_headers") {
  sources = [
    "enterprise_rollback_metrics_handler.h",
    "enterprise_rollback_metrics_tracking.h",
  ]
  install_path = "/usr/include/oobe_config/metrics"
}

install_config("install_proto_header") {
  sources = [ "${root_gen_dir}/include/oobe_config/metrics/enterprise_rollback_metrics_data.pb.h" ]
  install_path = "/usr/include/oobe_config/metrics/"
  deps = [ ":enterprise_rollback_metrics_proto" ]
}

install_config("install_headers-test") {
  sources = [ "enterprise_rollback_metrics_handler_for_testing.h" ]
  install_path = "/usr/include/oobe_config/metrics"
}

generate_pkg_config("libenterprise_rollback_metrics-test_pc") {
  name = "libenterprise_rollback_metrics-test"
  output_name = "libenterprise_rollback_metrics-test"
  description = "Enterprise Rollback metrics library for testing"
  version = "0.0"

  libs = [ "-lenterprise_rollback_metrics-test" ]
  install = true
}

source_set("enterprise_rollback_metrics_library_test_sources") {
  sources = [ "enterprise_rollback_metrics_handler_for_testing.cc" ]
  configs += [ "//common-mk:test" ]
  deps = [ ":enterprise_rollback_metrics_library_common_sources" ]

  # PIC is required for shared libraries and their dependencies.
  configs += [ "//common-mk:pic" ]
}

shared_library("libenterprise_rollback_metrics-test") {
  configs += [
    "//common-mk:test",
    ":target_defaults",
  ]
  deps = [
    ":enterprise_rollback_metrics_library_test_sources",
    ":libenterprise_rollback_metrics-test_pc",
    "//oobe_config/filesystem:filesystem_for_testing",
  ]
  if (use.cros_host) {
    install_path = "lib"
  } else {
    install_path = "/build/lib"
  }
}

if (use.test) {
  executable("enterprise_rollback_metrics_tests") {
    sources = [
      "enterprise_rollback_metrics_handler_test.cc",
      "enterprise_rollback_metrics_recorder_test.cc",
      "enterprise_rollback_metrics_tracking_test.cc",
    ]
    configs += [
      "//common-mk:test",
      ":target_defaults",
    ]
    run_test = true
    deps = [
      ":enterprise_rollback_metrics_library_test_sources",
      "//common-mk/testrunner",
      "//oobe_config/filesystem:filesystem_for_testing",
    ]
  }
}
