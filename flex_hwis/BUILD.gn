# Copyright 2023 The ChromiumOS Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/pkg_config.gni")

group("all") {
  deps = [
    ":flex_hwis_tool",
    ":libflex_hwis",
  ]
  if (use.test) {
    deps += [ ":flex_hwis_test" ]
  }
}

pkg_config("target_defaults") {
  pkg_deps = [
    "libbrillo",
    "libchrome",
  ]
}

static_library("libflex_hwis") {
  sources = [ "flex_hwis.cc" ]
  configs += [ ":target_defaults" ]
}

executable("flex_hwis_tool") {
  sources = [ "flex_hwis_tool.cc" ]
  install_path = "bin"
  configs += [ ":target_defaults" ]
  deps = [ ":libflex_hwis" ]
}

if (use.test) {
  pkg_config("flex_hwis_test_config") {
    pkg_deps = [ "libchrome-test" ]
  }

  executable("flex_hwis_test") {
    sources = [ "flex_hwis_test.cc" ]
    configs += [
      "//common-mk:test",
      ":flex_hwis_test_config",
      ":target_defaults",
    ]
    run_test = true
    deps = [
      ":libflex_hwis",
      "//common-mk/testrunner",
    ]
  }
}
