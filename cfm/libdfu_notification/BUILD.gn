# Copyright 2020 The Chromium OS Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/pkg_config.gni")

group("all") {
  deps = [
    ":libdfu_notification",
  ]
  if (use.test) {
    deps += [ ":libdfu_notification_test" ]
  }
}

pkg_config("libdfu_notification_pkg_deps") {
  pkg_deps = [ "libchrome-${libbase_ver}" ]
}

shared_library("libdfu_notification") {
  configs += [ ":libdfu_notification_pkg_deps" ]
  sources = [
    "dfu_log_notification.cc",
    "idfu_notification.cc",
  ]
}

if (use.test) {
  pkg_config("test_config") {
    pkg_deps = [ "libchrome-test-${libbase_ver}" ]
  }

  executable("libdfu_notification_test") {
    configs += [
      # //common-mk:test should be at the top. crbug.com/887845
      # TODO(crbug.com/887845): Remove this note after library order issue is resolved.
      "//common-mk:test",
      ":test_config",
    ]
    sources = [
      "dfu_log_notification.cc",
      "dfu_log_notification_test.cc",
      "idfu_notification.cc",
    ]
    defines = [ "UNIT_TEST" ]
    deps = [
      "//common-mk/testrunner",
    ]
  }
}
