# Copyright 2021 The ChromiumOS Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/pkg_config.gni")

group("all") {
  deps = [ ":fusebox" ]
  if (use.test) {
    deps += [ ":fusebox_test" ]
  }
}

pkg_config("target_defaults") {
  # NOSORT
  pkg_deps = [
    "libbrillo",
    "libchrome",
    "libcap",
    "fuse",

    # system_api depends on protobuf (or protobuf-lite). It must appear
    # before protobuf here or the linker flags won't be in the right
    # order.
    "system_api",
    "protobuf-lite",
  ]

  # Define needed to configure <fuse_lowlevel.h>.
  defines = [ "FUSE_USE_VERSION=26" ]
}

static_library("libfusebox") {
  sources = [
    "built_in.cc",
    "built_in.h",
    "file_system.cc",
    "file_system.h",
    "file_system_fuse.cc",
    "fuse_path_inodes.cc",
    "fuse_path_inodes.h",
    "fuse_request.cc",
    "fuse_request.h",
    "make_stat.cc",
    "make_stat.h",
    "util.cc",
    "util.h",
  ]
  configs += [ ":target_defaults" ]
}

executable("fusebox") {
  sources = [
    "fuse_frontend.h",
    "main.cc",
  ]
  configs += [ ":target_defaults" ]
  deps = [ ":libfusebox" ]
}

if (use.test) {
  pkg_config("fusebox_test_config") {
    pkg_deps = [ "libchrome-test" ]
  }

  executable("fusebox_test") {
    sources = [
      "fuse_path_inodes_test.cc",
      "make_stat_test.cc",
      "test_runner.cc",
      "util_test.cc",
    ]
    configs += [
      "//common-mk:test",
      ":target_defaults",
      ":fusebox_test_config",
    ]
    deps = [ ":libfusebox" ]
  }
}
