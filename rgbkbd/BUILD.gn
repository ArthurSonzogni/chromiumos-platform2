# Copyright 2022 The Chromium OS Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/pkg_config.gni")

group("all") {
  deps = [ ":rgbkbd" ]
  if (use.test) {
    deps += [
      ":install_dbus_activation",
      ":install_dbus_config",
      ":install_upstart_config",
      ":rgbkbd_test",
    ]
  }
}

pkg_config("target_defaults") {
  # NOSORT
  pkg_deps = [
    "libbrillo",
    "libchrome",

    # system_api depends on protobuf (or protobuf-lite). It must appear
    # before protobuf here or the linker flags won't be in the right
    # order.
    "system_api",
  ]
}

static_library("libdbus_service") {
  sources = [ "dbus_service.cc" ]
  configs += [ ":target_defaults" ]
}

executable("rgbkbd") {
  sources = [ "main.cc" ]
  configs += [ ":target_defaults" ]
  install_path = "sbin"
  deps = [ ":libdbus_service" ]
}

# Install upstart configs and scripts.
install_config("install_upstart_config") {
  sources = [ "init/rgbkbd.conf" ]
  install_path = "/etc/init"
}

# Install D-Bus configuration file.
install_config("install_dbus_config") {
  sources = [ "dbus/org.chromium.Rgbkbd.conf" ]
  install_path = "/etc/dbus-1/system.d"
}

# Install D-Bus service activation configuration.
install_config("install_dbus_activation") {
  sources = [ "dbus/org.chromium.Rgbkbd.service" ]
  install_path = "/usr/share/dbus-1/system-services"
}

if (use.test) {
  executable("rgbkbd_test") {
    sources = [ "dbus_service_test.cc" ]
    configs += [
      "//common-mk:test",
      ":target_defaults",
    ]
    run_test = true
    deps = [
      ":libdbus_service",
      "//common-mk/testrunner",
    ]
  }
}
