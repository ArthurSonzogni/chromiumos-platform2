# Copyright 2019 The Chromium OS Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/generate-dbus-proxies.gni")
import("//common-mk/mojom_bindings_generator.gni")

group("all") {
  deps = [
    ":libcamera_hal",
  ]
  if (use.test) {
    deps += [ ":request_queue_test" ]
  }
}

generate_dbus_proxies("libcamera_hal_dbus") {
  proxy_output_file = "include/dbus_proxies/dbus-proxies.h"
  sources = [
    "dbus/org.chromium.IpPeripheralService.xml",
  ]
}

generate_mojom_bindings("libcamera_hal_mojom") {
  sources = [
    "//camera/mojo/ip/ip_camera.mojom",
  ]
  mojo_root = "//camera"
}

shared_library("libcamera_hal") {
  sources = [
    "camera_device.cc",
    "camera_hal.cc",
    "metadata_handler.cc",
    "request_queue.cc",
  ]
  configs += [ "//camera/build:cros_camera_common" ]
  pkg_deps = [
    "cros-camera-android-headers",
    "libbrillo-${libbase_ver}",
    "libcamera_common",
    "libcamera_metadata",
    "libsync",
  ]
  deps = [
    ":libcamera_hal_dbus",
    ":libcamera_hal_mojom",
    "//camera/common/libcamera_ipc:libcamera_ipc",
  ]
}

if (use.test) {
  executable("request_queue_test") {
    sources = [
      "request_queue.cc",
      "request_queue_test.cc",
    ]
    configs += [
      "//camera/build:cros_camera_common",
      "//common-mk:test",
    ]
    pkg_deps = [
      "cros-camera-android-headers",
      "libcamera_metadata",
      "libsync",
    ]
    deps = [
      "//common-mk/testrunner",
    ]
  }
}
