# Copyright 2021 The Chromium OS Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

group("all") {
  deps = [
    ":hdrnet_processor_benchmark",
    ":hdrnet_processor_impl_test",
    ":hdrnet_stream_manipulator_test",
  ]
}

pkg_config("target_defaults") {
  configs = [
    "//camera/build:cros_camera_common",
    "//common-mk:test",
  ]
  pkg_deps = [
    "benchmark",
    "libchrome-test",
    "libcros_camera",
    "libcros_camera_android_deps",
    "libdrm",
  ]
  defines = [
    "USE_IPU6=${use.ipu6}",
    "USE_IPU6EP=${use.ipu6ep}",
  ]
}

source_set("hdrnet_processor_test_fixture") {
  sources =
      [ "//camera/features/hdrnet/tests/hdrnet_processor_test_fixture.cc" ]
  configs += [ ":target_defaults" ]
  deps = [
    "//camera/features/hdrnet",
    "//camera/gpu:gpu_test_support",
  ]
}

executable("hdrnet_processor_benchmark") {
  sources = [ "//camera/features/hdrnet/tests/hdrnet_processor_benchmark.cc" ]
  configs += [ ":target_defaults" ]
  deps = [ ":hdrnet_processor_test_fixture" ]
}

executable("hdrnet_processor_impl_test") {
  sources = [ "//camera/features/hdrnet/tests/hdrnet_processor_impl_test.cc" ]
  configs += [ ":target_defaults" ]
  deps = [ ":hdrnet_processor_test_fixture" ]
}

executable("hdrnet_stream_manipulator_test") {
  sources =
      [ "//camera/features/hdrnet/tests/hdrnet_stream_manipulator_test.cc" ]
  configs += [ ":target_defaults" ]
  deps = [
    "//camera/common:base",
    "//camera/features/hdrnet",
  ]
}
