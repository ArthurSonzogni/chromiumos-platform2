# Copyright 2022 The ChromiumOS Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/pkg_config.gni")

group("lib") {
  deps = [
    ":headers",
    ":libmojo_service_manager",
    ":mojo_service_manager",
  ]
}

pkg_config("target_default") {
  pkg_deps = [
    "libbrillo",
    "libchrome",
    "libmojo",
  ]
}

# Shared by shared_library and unit test targets.
source_set("libmojo_service_manager_source_set") {
  sources = [ "connect.cc" ]
  configs += [
    ":target_default",
    "//common-mk:pic",
  ]
  deps = [ "//mojo_service_manager/lib/mojom:libmojo_service_manager-mojom" ]
}

shared_library("libmojo_service_manager") {
  configs += [ ":target_default" ]
  install_path = "lib"
  deps = [ ":libmojo_service_manager_source_set" ]
}

generate_pkg_config("mojo_service_manager") {
  description = "API for communicating with mojo service manager."
  version = "0.1"
  requires_private = [
    "libchrome",
    "libmojo",
  ]
  libs = [
    "-lmojo_service_manager",
    "-lmojo_service_manager-mojom",
  ]
  cflags = [ "-I/usr/include/mojo_service_manager" ]
  install = true
}

install_config("headers") {
  sources = [ "connect.h" ]
  install_path = "/usr/include/mojo_service_manager/mojo_service_manager/lib"
}
