# Copyright 2022 The Chromium OS Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/pkg_config.gni")

group("daemon") {
  deps = [ ":mojo_service_manager" ]
  if (use.test) {
    deps += [ ":mojo_service_manager_test" ]
  }
}

pkg_config("target_default") {
  pkg_deps = [
    "libbrillo",
    "libchrome",
    "libmojo",
  ]
}

source_set("libmojo_service_manager_daemon") {
  sources = [
    "service_policy.cc",
    "service_policy_loader.cc",
  ]
  configs += [ ":target_default" ]
  deps = [ "//mojo_service_manager/lib/mojom" ]
}

executable("mojo_service_manager") {
  sources = [
    "daemon.cc",
    "main.cc",
  ]
  configs += [ ":target_default" ]
  install_path = "bin"
  deps = [
    ":libmojo_service_manager_daemon",
    "//mojo_service_manager/lib/mojom",
  ]
}

if (use.test) {
  pkg_config("target_test") {
    pkg_deps = [
      "libchrome",
      "libchrome-test",
      "libmojo",
    ]
  }

  executable("mojo_service_manager_test") {
    sources = [
      "mojo_test_runner.cc",
      "service_policy_loader_test.cc",
      "service_policy_test.cc",
      "service_policy_test_util.cc",
    ]
    configs += [
      ":target_test",
      "//common-mk:test",
    ]
    run_test = true
    deps = [
      ":libmojo_service_manager_daemon",
      "//common-mk/testrunner:testrunner",
    ]
  }
}
