# Copyright 2022 The ChromiumOS Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/pkg_config.gni")

group("daemon") {
  deps = [ ":mojo_service_manager" ]
  if (use.test) {
    deps += [ ":mojo_service_manager_test" ]
  }
}

pkg_config("target_default") {
  pkg_deps = [
    "libbrillo",
    "libchrome",
    "libmojo",
    "vboot_host",
  ]
}

source_set("libmojo_service_manager_daemon") {
  sources = [
    "daemon.cc",
    "mojo_error_util.cc",
    "service_manager.cc",
    "service_policy.cc",
    "service_policy_loader.cc",
    "service_request_queue.cc",
  ]
  configs += [ ":target_default" ]
  deps = [ "//mojo_service_manager/lib/mojom:libmojo_service_manager-mojom" ]
}

executable("mojo_service_manager") {
  sources = [ "main.cc" ]
  configs += [ ":target_default" ]
  install_path = "bin"
  deps = [
    ":libmojo_service_manager_daemon",
    "//mojo_service_manager/lib/mojom:libmojo_service_manager-mojom",
  ]
}

if (use.test) {
  pkg_config("target_test") {
    pkg_deps = [
      "libchrome",
      "libchrome-test",
      "libmojo",
    ]
  }

  executable("daemon_test_helper") {
    sources = [ "daemon_test_helper.cc" ]
    configs += [
      ":target_test",
      "//common-mk:test",
    ]
    deps = [
      ":libmojo_service_manager_daemon",
      "//mojo_service_manager/lib:libmojo_service_manager_source_set",
    ]
  }

  executable("mojo_service_manager_test") {
    sources = [
      "daemon_test.cc",
      "service_manager_test.cc",
      "service_policy_loader_test.cc",
      "service_policy_test.cc",
      "service_policy_test_util.cc",
      "service_request_queue_test.cc",
    ]
    configs += [
      ":target_test",
      "//common-mk:test",
    ]
    run_test = true
    deps = [
      ":daemon_test_helper",
      ":libmojo_service_manager_daemon",
      "//mojo_service_manager/testing:mojo_test_runner",
      "//mojo_service_manager/testing:test_mojom",
    ]
  }
}
