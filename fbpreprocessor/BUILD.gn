# Copyright 2023 The ChromiumOS Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/generate-dbus-adaptors.gni")
import("//common-mk/pkg_config.gni")
import("//common-mk/proto_library.gni")

group("all") {
  deps = [
    ":fbpreprocessor-proto",
    ":fbpreprocessord",
    ":install_dbus_config",
    ":install_init",
    ":install_tmpfiles_config",
    ":libfbpreprocessord",
  ]
  if (use.test) {
    deps += [ ":fbpreprocessord_test_runner" ]
  }
}

pkg_config("target_defaults") {
  defines = []

  # NOSORT
  pkg_deps = [
    "libbrillo",
    "libchrome",
    "protobuf",
    "system_api",
  ]
}

generate_dbus_adaptors("fbpreprocessor-dbus-adaptors") {
  dbus_service_config = "dbus_bindings/dbus-service-config.json"
  dbus_adaptors_out_dir = "include/fbpreprocessor/dbus_adaptors"
  sources = [ "dbus_bindings/org.chromium.FbPreprocessor.xml" ]
}

proto_library("fbpreprocessor-proto") {
  proto_in_dir = "${sysroot}/usr/include/chromeos/dbus/fbpreprocessor"
  proto_out_dir = "include"
  sources = [ "${proto_in_dir}/fbpreprocessor.proto" ]
}

static_library("libfbpreprocessord") {
  sources = [
    "dbus_adaptor.cc",
    "fbpreprocessor_daemon.cc",
    "firmware_dump.cc",
    "input_manager.cc",
    "manager.cc",
    "output_manager.cc",
    "pseudonymization_manager.cc",
    "session_state_manager.cc",
  ]
  configs += [ ":target_defaults" ]
  deps = [
    ":fbpreprocessor-dbus-adaptors",
    ":fbpreprocessor-proto",
  ]
}

executable("fbpreprocessord") {
  sources = [ "main.cc" ]
  configs += [ ":target_defaults" ]
  install_path = "bin"
  deps = [ ":libfbpreprocessord" ]
}

install_config("install_init") {
  sources = [ "init/fbpreprocessord.conf" ]
  install_path = "upstart"
}

install_config("install_tmpfiles_config") {
  sources = [ "tmpfiles.d/fbpreprocessord.conf" ]
  install_path = "tmpfilesd"
}

install_config("install_dbus_config") {
  sources = [ "dbus/org.chromium.FbPreprocessor.conf" ]
  install_path = "dbus_system_d"
}

if (use.test) {
  pkg_config("libchrome_test_config") {
    pkg_deps = [ "libchrome-test" ]
  }
  executable("fbpreprocessord_test_runner") {
    sources = [ "firmware_dump_test.cc" ]
    configs += [
      "//common-mk:test",
      ":libchrome_test_config",
      ":target_defaults",
    ]
    run_test = true
    deps = [
      ":libfbpreprocessord",
      "//common-mk/testrunner",
    ]
    pkg_deps = [ "dbus-1" ]
  }
}
