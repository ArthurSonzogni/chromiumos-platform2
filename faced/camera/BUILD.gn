# Copyright 2022 The ChromiumOS Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

group("all") {
  deps = [ ":camera" ]
  if (use.test) {
    deps += [ ":camera_unittests" ]
  }
}

pkg_config("target_defaults") {
  pkg_deps = [
    "absl",
    "libbrillo",
    "libchrome",
    "libcros_camera",
  ]
}

source_set("camera") {
  sources = [
    "camera_client.cc",
    "cros_camera_manager.cc",
    "cros_camera_service.cc",
    "fake_camera_client.cc",
    "fake_camera_service.cc",
    "frame_utils.cc",
    "test_utils.cc",
  ]
  public = [
    "camera_client.h",
    "camera_manager.h",
    "camera_service.h",
    "cros_camera_manager.h",
    "cros_camera_service.h",
    "fake_camera_client.h",
    "fake_camera_service.h",
    "frame_utils.h",
    "test_utils.h",
  ]
  configs += [ ":target_defaults" ]
}

if (use.test) {
  pkg_config("test_pkg_deps") {
    pkg_deps = [ "libchrome-test" ]
  }

  executable("camera_unittests") {
    sources = [
      "camera_client_test.cc",
      "cros_camera_manager_test.cc",
      "fake_camera_client_test.cc",
    ]
    configs += [
      "//common-mk:test",
      ":target_defaults",
      ":test_pkg_deps",
    ]
    run_test = true
    deps = [
      ":camera",
      "//common-mk/testrunner",
    ]
  }
}
