# Copyright 2021 The ChromiumOS Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/pkg_config.gni")

group("all") {
  deps = [
    ":install_dbus_activation",
    ":install_dbus_config",
    ":install_seccomp_policy",
    ":install_upstart_config",
    ":rmad",
  ]
  if (use.test) {
    deps += [ ":rmad_test" ]
  }
}

pkg_config("target_defaults") {
  pkg_deps = [
    "libbrillo",
    "libchrome",
  ]
}

executable("rmad") {
  sources = [ "main.cc" ]
  configs += [ ":target_defaults" ]
  install_path = "sbin"
  deps = [
    "daemon:libdaemon",
    "executor:libexecutor",
    "interface:libinterface",
    "minijail:libminijail",
    "utils:libutils",
  ]
}

# Install upstart configs and scripts.
install_config("install_upstart_config") {
  sources = [ "init/rmad.conf" ]
  install_path = "/etc/init"
}

# Install D-Bus configuration file.
install_config("install_dbus_config") {
  sources = [ "dbus/org.chromium.Rmad.conf" ]
  install_path = "/etc/dbus-1/system.d"
}

# Install D-Bus service activation configuration.
install_config("install_dbus_activation") {
  sources = [ "dbus/org.chromium.Rmad.service" ]
  install_path = "/usr/share/dbus-1/system-services"
}

# Install seccomp policy file.
install_config("install_seccomp_policy") {
  sources = [ "seccomp/rmad-seccomp-" + getenv("ARCH") + ".policy" ]
  install_path = "/usr/share/policy"
  outputs = [ "rmad-seccomp.policy" ]
}

if (use.test) {
  executable("rmad_test") {
    run_test = true
    deps = [
      "daemon:libdaemon_test",
      "executor:libexecutor_test",
      "interface:libinterface_test",
      "metrics:libmetrics_test",
      "state_handler:libstate_handler_test",
      "system:libsystem_test",
      "utils:libutils_test",
      "//common-mk/testrunner",
    ]
  }
}
