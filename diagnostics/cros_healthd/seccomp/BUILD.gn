# Copyright 2023 The ChromiumOS Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//diagnostics/cros_healthd/seccomp/check_seccomp.gni")

group("seccomp") {
  deps = [
    ":diagnostics_seccomp",
    ":diagnostics_seccomp_delegate",
  ]
}

BASE_REQUIRED_SYSCALLS = [
  # These time syscalls are usually used in some conditions (e.g. logging).
  "clock_getres",
  "clock_gettime",
  "gettimeofday",
  "exit_group",
]

if (getenv("ARCH") == "arm") {
  BASE_REQUIRED_SYSCALLS += [
    "clock_getres_time64",
    "clock_gettime64",
  ]
}

if (getenv("ARCH") == "arm64") {
  BASE_REQUIRED_SYSCALLS += [
    # `brk` is used by malloc.
    "brk",

    # `futex` is used in `libc_start_call_main`.
    "futex",
  ]
}

BASE_DENIED_SYSCALLS = [
  # This often be added because of the syscall that minijail execute the
  # jailed process, which is not needed.
  "execve",

  # These syscalls are not needed but often added because the seccomp is
  # generated by running `minijail0`.
  "chroot",
  "pivot_root",
  "mount",
]

install_and_check_seccomp_policy("diagnostics_seccomp") {
  _arch = getenv("ARCH")
  sources = [
    "${_arch}/btmon-seccomp.policy",
    "${_arch}/cros_healthd-seccomp.policy",
    "${_arch}/fio-seccomp.policy",
    "${_arch}/hciconfig-seccomp.policy",
    "${_arch}/healthd_rm-seccomp.policy",
    "${_arch}/iw-seccomp.policy",
    "${_arch}/memtester-seccomp.policy",
    "${_arch}/stressapptest-seccomp.policy",
  ]
  install_path = "seccomp_policy"

  required_syscalls = BASE_REQUIRED_SYSCALLS
  denied_syscalls = BASE_DENIED_SYSCALLS
}

install_and_check_seccomp_policy("diagnostics_seccomp_delegate") {
  _arch = getenv("ARCH")
  sources = [
    "${_arch}/drm-seccomp.policy",
    "${_arch}/ec_fan-seccomp.policy",
    "${_arch}/ec_i2cread-seccomp.policy",
    "${_arch}/ec_led-seccomp.policy",
    "${_arch}/ec_lid_angle-seccomp.policy",
    "${_arch}/ec_thermal-seccomp.policy",
    "${_arch}/evdev-seccomp.policy",
    "${_arch}/fingerprint-seccomp.policy",
    "${_arch}/floating_point-seccomp.policy",
    "${_arch}/ndt-seccomp.policy",
    "${_arch}/prime_search-seccomp.policy",
    "${_arch}/readonly-fetchers-seccomp.policy",
    "${_arch}/touchpad_fetcher-seccomp.policy",
    "${_arch}/urandom-seccomp.policy",
  ]
  if (_arch == "amd64") {
    sources += [ "${_arch}/psr-seccomp.policy" ]
  }

  # b/354620845: The reven board uses a different graphics driver than most
  # ChromeOS device, and requires a different set of IOCTL arguments. There
  # is very limited coverage of flex device within the labs, so graphics fetcher
  # often crashes on real devices. The reven-specific seccomp policy enables all
  # IOCTL arguments for graphics fetcher in reven board.
  if (use.mesa_reven) {
    sources += [ "reven/egl-seccomp.policy" ]
  } else {
    sources += [ "${_arch}/egl-seccomp.policy" ]
  }

  install_path = "seccomp_policy"

  required_syscalls = BASE_REQUIRED_SYSCALLS + [
                        # These syscalls are required by all executor delegates.
                        # Filter arg2 to disallow GRND_RANDOM (2).
                        "getrandom: arg2 in ~GRND_RANDOM",

                        # Required for syslog.
                        "connect",
                        "sendmsg: 1",
                        "socket: arg0 == AF_UNIX",
                        "socketpair: arg0 == AF_UNIX",
                      ]
  if (_arch == "arm") {
    required_syscalls += [
      # `futex` is used in `libc_start_call_main`.
      "futex",
    ]
  }
  if (_arch == "arm64") {
    required_syscalls += [ "process_vm_readv" ]
  }
  denied_syscalls = BASE_DENIED_SYSCALLS
}
