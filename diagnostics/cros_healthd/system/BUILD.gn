# Copyright 2020 The Chromium OS Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/pkg_config.gni")

# Library that provides system-level utilities used by cros_healthd.
pkg_config("libcros_healthd_system_pkg_deps") {
  pkg_deps = [
    "dbus-1",
    "libattestation-client",
    "libbrillo",
    "libchrome",
    "libcros_config",
    "libdebugd-client",
    "libmojo",
    "libpci",
    "libtpm_manager-client",
    "libudev",
    "protobuf",
    "system_api",
  ]
}

config("pcilib_config") {
  cflags = [ "-Wno-format-nonliteral" ]
}

source_set("libcros_healthd_system_pci_util") {
  configs += [
    ":libcros_healthd_system_pkg_deps",
    ":pcilib_config",
  ]
  sources = [ "pci_util_impl.cc" ]
}

source_set("libcros_healthd_system") {
  configs += [ ":libcros_healthd_system_pkg_deps" ]
  deps = [
    ":libcros_healthd_system_pci_util",
    "//diagnostics:dbus-proxies",
    "//diagnostics/common:libcommon",
    "//diagnostics/cros_healthd/executor:libcros_healthd_executor",
    "//diagnostics/cros_healthd/network:libnetwork_health_adapter",
    "//diagnostics/cros_healthd/network_diagnostics:libnetwork_diagnostics_adapter",
    "//diagnostics/mojom/external:cros_healthd_internal",
  ]
  sources = [
    "context.cc",
    "system_config.cc",
    "system_config_constants.cc",
    "system_utilities_constants.cc",
    "system_utilities_impl.cc",
  ]
}

if (use.test || use.fuzzer) {
  # Libraries for unit tests and fuzzers.
  pkg_config("libcros_healthd_system_test_utils_pkg_deps") {
    pkg_deps = [
      "dbus-1",
      "libattestation-client",
      "libattestation-client-test",
      "libchrome",
      "libchrome-test",
      "libcros_config",
      "libdebugd-client",
      "libdebugd-client-test",
      "libtpm_manager-client",
      "libtpm_manager-client-test",
      "protobuf",
      "system_api",
    ]
  }

  source_set("libcros_healthd_system_test_utils") {
    configs += [
      ":libcros_healthd_system_test_utils_pkg_deps",
      "//common-mk:test",
    ]
    deps = [
      ":libcros_healthd_system",
      "//diagnostics/common:libcommon_test_utils",
      "//diagnostics/cros_healthd/executor:libcros_healthd_executor_test_utils",
      "//diagnostics/cros_healthd/network:libnetwork_health_adapter_test_utils",
      "//diagnostics/cros_healthd/network_diagnostics:libnetwork_diagnostics_adapter_test_utils",
    ]
    sources = [
      "fake_pci_util.cc",
      "fake_system_config.cc",
      "fake_system_utilities.cc",
      "mock_context.cc",
    ]
  }
}

if (use.test) {
  pkg_config("system_config_test_pkg_deps") {
    pkg_deps = [
      "libchrome",
      "libchrome-test",
      "libcros_config",
    ]
  }

  source_set("system_config_test") {
    sources = [ "system_config_test.cc" ]
    configs += [
      ":system_config_test_pkg_deps",
      "//common-mk:test",
    ]
    deps = [
      ":libcros_healthd_system",
      "//common-mk/testrunner:testrunner",
      "//diagnostics/common:libcommon_test_utils",
    ]
  }
}
