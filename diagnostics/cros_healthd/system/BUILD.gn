# Copyright 2020 The Chromium OS Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/pkg_config.gni")

# Library that provides system-level utilities used by cros_healthd.
pkg_config("libcros_healthd_system_pkg_deps") {
  pkg_deps = [
    "dbus-1",
    "libbrillo",
    "libchrome-${libbase_ver}",
    "libcros_config",
    "libdebugd-client",
    "system_api",
  ]
}

source_set("libcros_healthd_system") {
  deps = [ "//diagnostics/common:libcommon" ]
  all_dependent_configs = [ ":libcros_healthd_system_pkg_deps" ]
  sources = [ "context.cc" ]
}

if (use.test) {
  # Libraries for unit tests.
  pkg_config("libcros_healthd_system_test_utils_pkg_deps") {
    pkg_deps = [
      "dbus-1",
      "libchrome-${libbase_ver}",
      "libcros_config",
      "libdebugd-client-test",
      "system_api",
    ]
  }

  source_set("libcros_healthd_system_test_utils") {
    deps = [
      ":libcros_healthd_system",
      "//diagnostics/common:libcommon_test_utils",
    ]
    all_dependent_configs = [ ":libcros_healthd_system_test_utils_pkg_deps" ]
    sources = [ "mock_context.cc" ]
  }
}
