# Copyright 2020 The ChromiumOS Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/pkg_config.gni")

# Library that provides system-level utilities used by cros_healthd.
pkg_config("system_pkg_deps") {
  pkg_deps = [
    "dbus-1",
    "iioservice_ipc",
    "libattestation-client",
    "libbrillo",
    "libchrome",
    "libcros_camera",
    "libdebugd-client",
    "libmojo",
    "libpci",
    "libpower_manager-client",
    "libspaced",
    "libtpm_manager-client",
    "libudev",
    "mojo_service_manager",
    "protobuf",
    "system_api",
  ]
}

config("pcilib_config") {
  cflags = [ "-Wno-format-nonliteral" ]
}

source_set("system_pci_util") {
  configs += [
    ":system_pkg_deps",
    ":pcilib_config",
  ]
  sources = [ "pci_util_impl.cc" ]
}

source_set("system") {
  configs += [ ":system_pkg_deps" ]
  public_deps = [ "//diagnostics/mojom/public:libcros_healthd_mojo_bindings" ]
  deps = [
    ":system_pci_util",
    "//diagnostics/base",
    "//diagnostics/cros_healthd/mojom:executor_mojo_bindings",
    "//diagnostics/cros_healthd/utils",
    "//diagnostics/dbus_bindings",
    "//diagnostics/mojom/external:libcros_healthd_internal",
  ]
  sources = [
    "bluez_controller.cc",
    "bluez_event_hub.cc",
    "context.cc",
    "cros_config.cc",
    "floss_callback_services.cc",
    "floss_controller.cc",
    "floss_event_hub.cc",
    "ground_truth.cc",
    "mojo_service_impl.cc",
    "powerd_adapter_impl.cc",
    "system_config.cc",
    "system_utilities_impl.cc",
    "usb_device_info.cc",
  ]
}

if (use.test || use.fuzzer) {
  # Libraries for unit tests and fuzzers.
  pkg_config("test_support_pkg_deps") {
    pkg_deps = [
      "dbus-1",
      "libattestation-client",
      "libattestation-client-test",
      "libchrome",
      "libchrome-test",
      "libdebugd-client",
      "libdebugd-client-test",
      "libpower_manager-client",
      "libpower_manager-client-test",
      "libspaced",
      "libspaced-test",
      "libtpm_manager-client",
      "libtpm_manager-client-test",
      "protobuf",
      "system_api",
    ]
  }

  pkg_config("test_support_public_pkg_deps") {
    pkg_deps = [
      "iioservice_ipc",
      "libcros_camera",
      "mojo_service_manager",
    ]
  }

  source_set("test_support") {
    configs += [
      ":test_support_pkg_deps",
      "//common-mk:test",
    ]
    public_configs = [ ":test_support_public_pkg_deps" ]
    public_deps = [ "//diagnostics/cros_healthd/fake" ]
    deps = [
      ":system",
      "//diagnostics/base",
    ]
    sources = [
      "fake_bluez_event_hub.cc",
      "fake_floss_event_hub.cc",
      "fake_mojo_service.cc",
      "fake_pci_util.cc",
      "fake_powerd_adapter.cc",
      "fake_system_config.cc",
      "fake_system_utilities.cc",
      "mock_context.cc",
    ]
  }
}

if (use.test) {
  pkg_config("system_test_pkg_deps") {
    pkg_deps = [
      "libchrome",
      "libchrome-test",
      "libdebugd-client",
      "libdebugd-client-test",
      "libpower_manager-client",
      "libpower_manager-client-test",
    ]
  }

  source_set("system_test") {
    sources = [
      "bluez_event_hub_test.cc",
      "cros_config_test.cc",
      "floss_event_hub_test.cc",
      "ground_truth_test.cc",
      "powerd_adapter_impl_test.cc",
      "system_config_test.cc",
      "usb_device_info_test.cc",
    ]
    configs += [ ":system_test_pkg_deps" ]
    deps = [
      ":system",
      ":test_support",
      "//diagnostics/base",
      "//diagnostics/base:healthd_testrunner",
      "//diagnostics/dbus_bindings",
    ]
  }
}
