# Copyright 2023 The ChromiumOS Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

pkg_config("libexecutor_delegate_pkg_deps") {
  pkg_deps = [
    "libbrillo",
    "libchrome",
    "libdrm",
    "libec",
    "libmojo",
  ]
}

source_set("libexecutor_delegate") {
  sources = [ "delegate_impl.cc" ]
  configs += [ ":libexecutor_delegate_pkg_deps" ]
  deps = [
    "//diagnostics/cros_healthd/delegate/fetchers",
    "//diagnostics/cros_healthd/delegate/routines",
    "//diagnostics/cros_healthd/delegate/utils",
    "//diagnostics/cros_healthd/mojom:cros_healthd_executor_mojo_bindings",
  ]
}

executable("executor-delegate") {
  sources = [ "delegate_main.cc" ]
  configs += [ ":libexecutor_delegate_pkg_deps" ]
  assert_no_deps = [
    "//diagnostics/cros_healthd:*",
    "//diagnostics/cros_healthd/event/*",
    "//diagnostics/cros_healthd/executor/*",
    "//diagnostics/cros_healthd/fetchers/*",
    "//diagnostics/cros_healthd/minijail/*",
    "//diagnostics/cros_healthd/process/*",
    "//diagnostics/cros_healthd/routines/*",
    "//diagnostics/cros_healthd/system/*",
    "//diagnostics/cros_healthd/utils/*",
    "//diagnostics/cros_healthd/welco_ec/*",
  ]
  install_path = "/usr/libexec/diagnostics"
  deps = [ ":libexecutor_delegate" ]
}

if (use.test) {
  pkg_config("test_pkg_deps") {
    pkg_deps = [
      "libbrillo",
      "libchrome",
      "libchrome-test",
      "libdrm",
      "libec",
      "libmojo",
    ]
  }

  source_set("test") {
    sources = [ "delegate_impl_test.cc" ]
    configs += [
      ":test_pkg_deps",
      "//common-mk:test",
    ]
    deps = [
      ":libexecutor_delegate",
      "//diagnostics/base",
    ]
  }
}
