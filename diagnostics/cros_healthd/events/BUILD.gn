# Copyright 2020 The Chromium OS Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/pkg_config.gni")

# Library that provides cros_healthd's event functionality.
pkg_config("libcros_healthd_events_pkg_deps") {
  pkg_deps = [
    "libbrillo",
    "libchrome-${libbase_ver}",
    "libmojo-${libbase_ver}",
    "protobuf",
    "system_api",
  ]
}

source_set("libcros_healthd_events") {
  deps = [
    "//diagnostics:diagnostics_mojo_bindings",
    "//diagnostics/common:libcommon",
  ]
  all_dependent_configs = [ ":libcros_healthd_events_pkg_deps" ]
  sources = [
    "bluetooth_events_impl.cc",
    "lid_events_impl.cc",
    "power_events_impl.cc",
  ]
}

if (use.test) {
  pkg_config("libcros_healthd_events_test_pkg_deps") {
    pkg_deps = [
      "libchrome-${libbase_ver}",
      "libchrome-test-${libbase_ver}",
      "libmojo-${libbase_ver}",
    ]
  }

  executable("libcros_healthd_events_test") {
    configs += [
      ":libcros_healthd_events_test_pkg_deps",
      "//common-mk:test",
    ]
    deps = [
      ":libcros_healthd_events",
      "//common-mk/testrunner:testrunner",
      "//diagnostics:diagnostics_mojo_bindings",
      "//diagnostics/common:libcommon_test_utils",
    ]
    sources = [
      "bluetooth_events_impl_test.cc",
      "lid_events_impl_test.cc",
      "power_events_impl_test.cc",
    ]
  }
}
