# Copyright 2019 The ChromiumOS Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/pkg_config.gni")

pkg_config("routines_pkg_config") {
  pkg_deps = [
    "libchrome",
    "libmojo",
    "mojo_service_manager",
  ]
}

group("routines") {
  deps = [
    ":utils",
    "//diagnostics/cros_healthd/routines/android_network",
    "//diagnostics/cros_healthd/routines/audio",
    "//diagnostics/cros_healthd/routines/battery_and_power",
    "//diagnostics/cros_healthd/routines/bluetooth",
    "//diagnostics/cros_healthd/routines/camera",
    "//diagnostics/cros_healthd/routines/fan",
    "//diagnostics/cros_healthd/routines/fingerprint",
    "//diagnostics/cros_healthd/routines/hardware_button",
    "//diagnostics/cros_healthd/routines/led",
    "//diagnostics/cros_healthd/routines/memory_and_cpu",
    "//diagnostics/cros_healthd/routines/network",
    "//diagnostics/cros_healthd/routines/privacy_screen",
    "//diagnostics/cros_healthd/routines/sensor",
    "//diagnostics/cros_healthd/routines/storage",
  ]
}

source_set("utils") {
  sources = [
    "base_routine_control.cc",
    "diag_routine_with_status.cc",
    "routine_adapter.cc",
    "routine_service.cc",
    "simple_routine.cc",
  ]
  configs += [ ":routines_pkg_config" ]
  public_deps = [ "//diagnostics/mojom/public:libcros_healthd_mojo_bindings" ]
  deps = [
    "//diagnostics/base",
    "//diagnostics/cros_healthd/system:libcros_healthd_system",
  ]
}

if (use.test) {
  source_set("libroutine_test_utils") {
    sources = [
      "routine_observer_for_testing.cc",
      "routine_test_utils.cc",
      "routine_v2_test_utils.cc",
    ]
    configs += [ "//common-mk:test" ]
    deps = [
      "//diagnostics/base",
      "//diagnostics/mojom/public:libcros_healthd_mojo_bindings",
    ]
  }

  source_set("routine_test") {
    sources = [
      "base_routine_control_test.cc",
      "diag_routine_with_status_test.cc",
      "routine_service_test.cc",
      "routine_v2_test_utils_test.cc",
      "simple_routine_test.cc",
    ]
    configs += [ ":routines_pkg_config" ]
    deps = [
      ":libroutine_test_utils",
      ":routines",
      "//diagnostics/base",
      "//diagnostics/base:healthd_testrunner",
      "//diagnostics/cros_healthd/system:libcros_healthd_system_test_utils",
    ]
  }
}
