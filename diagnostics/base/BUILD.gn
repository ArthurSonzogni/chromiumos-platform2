# Copyright 2023 The ChromiumOS Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

group("base") {
  public_deps = [ ":libdiagnostics_base" ]
}

# Provides basic utilities for all the executables under //diagnostics.
shared_library("libdiagnostics_base") {
  sources = [ "file_utils.cc" ]
  assert_no_deps = [
    "//diagnostics/bindings/*",
    "//diagnostics/common/*",
    "//diagnostics/constants/*",
    "//diagnostics/cros_health_tool/*",
    "//diagnostics/cros_healthd/*",
    "//diagnostics/mojom/*",
    "//diagnostics/wilco_dtc_supportd/*",
  ]
  install_path = "lib"
  pkg_deps = [
    "libbrillo",
    "libchrome",
  ]
}

if (use.test) {
  pkg_config("test_pkg_deps") {
    pkg_deps = [ "libchrome-test" ]
  }

  source_set("test") {
    sources = [
      "file_test_utils.cc",
      "file_test_utils_test.cc",
      "file_utils_test.cc",
    ]
    configs += [ ":test_pkg_deps" ]
    deps = [ ":libdiagnostics_base" ]
  }
}
