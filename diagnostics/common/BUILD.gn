# Copyright 2019 The ChromiumOS Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/pkg_config.gni")

# Library with utilities common to both cros_healthd and wilco_dtc_supportd.
pkg_config("libcommon_pkg_deps") {
  pkg_deps = [
    "libchrome",
    "libmojo",
  ]
}

source_set("libcommon") {
  configs += [ ":libcommon_pkg_deps" ]
  sources = [ "mojo_utils.cc" ]
}

if (use.test || use.fuzzer) {
  source_set("libcommon_test_utils") {
    configs += [ ":libcommon_pkg_deps" ]
    deps = [
      ":libcommon",
      "//diagnostics/mojom/public:cros_healthd_mojo_bindings",
    ]
    sources = [ "mojo_type_utils.cc" ]
  }
}

if (use.test) {
  source_set("libcommon_test") {
    sources = [
      "dbus_utils_test.cc",
      "mojo_utils_test.cc",
    ]
    configs += [ ":libcommon_pkg_deps" ]
    deps = [
      ":libcommon",
      ":libcommon_test_utils",
      "//diagnostics/base:healthd_testrunner",
    ]
  }
}
