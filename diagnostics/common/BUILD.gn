# Copyright 2019 The Chromium OS Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/pkg_config.gni")

# Library with utilities common to both cros_healthd and wilco_dtc_supportd.
pkg_config("libcommon_pkg_deps") {
  pkg_deps = [
    "libchrome-${libbase_ver}",
    "libmojo-${libbase_ver}",
  ]
}

source_set("libcommon") {
  public_configs = [ ":libcommon_pkg_deps" ]
  sources = [
    "mojo_utils.cc",
  ]
}

if (use.test) {
  # Library with common utilities only to be used during testing.
  pkg_config("libcommon_test_utils_pkg_deps") {
    pkg_deps = [
      "libchrome-${libbase_ver}",
      "libmojo-${libbase_ver}",
    ]
  }

  source_set("libcommon_test_utils") {
    deps = [
      ":libcommon",
    ]
    all_dependent_configs = [ ":libcommon_test_utils_pkg_deps" ]
    sources = [
      "file_test_utils.cc",
      "mojo_test_utils.cc",
    ]
  }

  pkg_config("libcommon_test_pkg_deps") {
    pkg_deps = [
      "libchrome-${libbase_ver}",
      "libchrome-test-${libbase_ver}",
      "libmojo-${libbase_ver}",
    ]
  }

  executable("libcommon_test") {
    configs += [
      ":libcommon_test_pkg_deps",
      "//common-mk:test",
    ]
    deps = [
      ":libcommon",
      "//common-mk/testrunner:testrunner",
    ]
    sources = [
      "mojo_utils_test.cc",
    ]
  }
}
