# Copyright 2019 The ChromiumOS Authors
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

group("all") {
  deps = [
    ":install_dbus_config",
    ":install_init_config",
    ":install_service_manager_policy_config",
    ":install_tmpfiles_config",
    "//diagnostics/cros_health_tool:cros-health-tool",
    "//diagnostics/cros_healthd:cros_healthd",
    "//diagnostics/cros_healthd/delegate:executor-delegate",
    "//diagnostics/cros_healthd/routines:urandom",
    "//diagnostics/cros_healthd/seccomp",
    "//diagnostics/dbus_bindings",
    "//diagnostics/mojom",
    "//diagnostics/wilco_dtc_supportd:wilco_dtc_supportd",
    "//diagnostics/wilco_dtc_supportd/seccomp",
  ]
  if (use.test) {
    deps += [
      "//diagnostics/cros_healthd:cros_healthd_test",
      "//diagnostics/wilco_dtc_supportd:wilco_dtc_supportd_test",
    ]
  }
  if (use.fuzzer) {
    deps += [
      "//diagnostics/cros_healthd/events:crash_events_uploads_log_parser_fuzzer",
      "//diagnostics/cros_healthd/fetchers:fetch_system_info_fuzzer",
    ]
  }
}

install_config("install_dbus_config") {
  sources = [ "dbus/org.chromium.CrosHealthd.conf" ]
  install_path = "dbus_system_d"
  if (use.wilco) {
    sources += [
      "dbus/WilcoDtcUpstart.conf",
      "dbus/org.chromium.WilcoDtcSupportd.conf",
    ]
  }
}

install_config("install_init_config") {
  sources = [ "init/cros_healthd.conf" ]
  install_path = "upstart"
  if (use.wilco) {
    sources += [
      "init/wilco_dtc.conf",
      "init/wilco_dtc_dispatcher.conf",
      "init/wilco_dtc_supportd.conf",
    ]
  }
}

install_config("install_tmpfiles_config") {
  sources = [ "init/tmpfiles.d/cros_healthd.conf" ]
  install_path = "tmpfiled_ondemand"
}

install_config("install_service_manager_policy_config") {
  sources = [ "init/mojo/cros_healthd.jsonc" ]
  install_path = "/etc/mojo/service_manager/policy"
}
