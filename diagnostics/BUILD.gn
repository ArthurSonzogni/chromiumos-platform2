# Copyright 2019 The Chromium OS Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/pkg_config.gni")

group("all") {
  deps = [
    ":cros_healthd",
    "//diagnostics/cros_healthd_event_tool:cros-health-event",
    "//diagnostics/diag:diag",
    "//diagnostics/routines:floating-point-accuracy",
    "//diagnostics/routines:prime-search",
    "//diagnostics/routines:smartctl-check",
    "//diagnostics/routines:urandom",
    "//diagnostics/telem:telem",
    "//diagnostics/wilco_dtc_supportd:wilco_dtc_supportd",
  ]
  if (use.test) {
    deps += [
      ":cros_healthd_test",
      ":libcros_healthd_utils_test",
      "//diagnostics/common:libcommon_test",
      "//diagnostics/cros_healthd/events:libcros_healthd_events_test",
      "//diagnostics/cros_healthd_mojo_adapter:cros_healthd_mojo_adapter_test",
      "//diagnostics/grpc_async_adapter:libgrpc_async_adapter_test",
      "//diagnostics/routines:routine_test",
      "//diagnostics/wilco_dtc_supportd:wilco_dtc_supportd_test",
    ]
  }
  if (use.fuzzer) {
    deps += [
      ":fetch_block_device_fuzzer",
      ":fetch_cached_vpd_fuzzer",
    ]
  }
}

# Library that provides utilities used by cros_healthd.
pkg_config("libcros_healthd_utils_pkg_deps") {
  pkg_deps = [
    "dbus-1",
    "libbrillo",
    "libchrome-${libbase_ver}",
    "libcros_config",
    "libdebugd-client",
    "libmojo-${libbase_ver}",
    "libudev",
    "protobuf",
    "re2",
    "system_api",
  ]
}

source_set("libcros_healthd_utils") {
  deps = [
    "//diagnostics/common:libcommon",
    "//diagnostics/cros_healthd/system:libcros_healthd_system",
    "//diagnostics/mojo:diagnostics_mojo_bindings",
  ]
  all_dependent_configs = [ ":libcros_healthd_utils_pkg_deps" ]
  sources = [
    "cros_healthd/utils/backlight_utils.cc",
    "cros_healthd/utils/battery_utils.cc",
    "cros_healthd/utils/cpu_file_helpers.cc",
    "cros_healthd/utils/cpu_utils.cc",
    "cros_healthd/utils/disk_utils.cc",
    "cros_healthd/utils/fan_utils.cc",
    "cros_healthd/utils/memory_utils.cc",
    "cros_healthd/utils/stateful_partition_utils.cc",
    "cros_healthd/utils/timezone_utils.cc",
    "cros_healthd/utils/vpd_utils.cc",
  ]
}

# Library that provides core functionality for the cros_healthd daemon.
pkg_config("libcros_healthd_pkg_deps") {
  pkg_deps = [
    "dbus-1",
    "libbrillo",
    "libchrome-${libbase_ver}",
    "libdebugd-client",
    "libmojo-${libbase_ver}",
  ]
}

source_set("libcros_healthd") {
  deps = [
    ":libcros_healthd_utils",
    "//diagnostics/cros_healthd/events:libcros_healthd_events",
    "//diagnostics/cros_healthd/system:libcros_healthd_system",
    "//diagnostics/mojo:diagnostics_mojo_bindings",
    "//diagnostics/routines:libdiag_routine",
  ]
  all_dependent_configs = [ ":libcros_healthd_pkg_deps" ]
  sources = [
    "cros_healthd/cros_healthd.cc",
    "cros_healthd/cros_healthd_mojo_service.cc",
    "cros_healthd/cros_healthd_routine_factory_impl.cc",
    "cros_healthd/cros_healthd_routine_service_impl.cc",
  ]
}

# The cros_healthd daemon executable.
pkg_config("cros_healthd_pkg_deps") {
  pkg_deps = [ "libbrillo" ]
}

executable("cros_healthd") {
  deps = [ ":libcros_healthd" ]
  sources = [ "cros_healthd/main.cc" ]
}

if (use.test) {
  # Libraries for unit tests.

  # Unit tests.
  pkg_config("cros_healthd_test_pkg_deps") {
    pkg_deps = [
      "libchrome-${libbase_ver}",
      "libchrome-test-${libbase_ver}",
      "libdebugd-client-test",
    ]
  }

  executable("cros_healthd_test") {
    configs += [
      ":cros_healthd_test_pkg_deps",
      "//common-mk:test",
    ]
    deps = [
      ":libcros_healthd",
      "../common-mk/testrunner:testrunner",
      "//diagnostics/common:libcommon_test_utils",
      "//diagnostics/cros_healthd/system:libcros_healthd_system_test_utils",
      "//diagnostics/routines:libroutine_test_utils",
    ]
    sources = [
      "cros_healthd/cros_healthd_mojo_service_test.cc",
      "cros_healthd/cros_healthd_routine_service_impl_test.cc",
      "cros_healthd/fake_cros_healthd_routine_factory.cc",
    ]
  }

  pkg_config("libcros_healthd_utils_test_pkg_deps") {
    pkg_deps = [
      "libchrome-${libbase_ver}",
      "libchrome-test-${libbase_ver}",
      "libcros_config",
      "libdebugd-client-test",
      "system_api",
    ]
  }

  executable("libcros_healthd_utils_test") {
    configs += [
      ":libcros_healthd_utils_test_pkg_deps",
      "//common-mk:test",
    ]
    deps = [
      ":libcros_healthd_utils",
      "../common-mk/testrunner:testrunner",
      "//diagnostics/common:libcommon_test_utils",
      "//diagnostics/cros_healthd/system:libcros_healthd_system_test_utils",
    ]
    sources = [
      "cros_healthd/utils/backlight_utils_test.cc",
      "cros_healthd/utils/battery_utils_test.cc",
      "cros_healthd/utils/cpu_file_helpers_test.cc",
      "cros_healthd/utils/cpu_utils_test.cc",
      "cros_healthd/utils/fan_utils_test.cc",
      "cros_healthd/utils/memory_utils_test.cc",
      "cros_healthd/utils/stateful_partition_utils_test.cc",
      "cros_healthd/utils/timezone_utils_test.cc",
      "cros_healthd/utils/vpd_utils_test.cc",
    ]
  }
}

if (use.fuzzer) {
  executable("fetch_block_device_fuzzer") {
    configs += [ "//common-mk/common_fuzzer:common_fuzzer" ]
    deps = [ ":libcros_healthd_utils" ]
    sources = [ "cros_healthd/utils/fetch_block_device_fuzzer.cc" ]
  }

  pkg_config("fetch_cached_vpd_fuzzer_pkg_deps") {
    pkg_deps = [ "libcros_config" ]
  }

  executable("fetch_cached_vpd_fuzzer") {
    configs += [
      ":fetch_cached_vpd_fuzzer_pkg_deps",
      "//common-mk/common_fuzzer:common_fuzzer",
    ]
    deps = [ ":libcros_healthd_utils" ]
    sources = [ "cros_healthd/utils/fetch_cached_vpd_fuzzer.cc" ]
  }
}
