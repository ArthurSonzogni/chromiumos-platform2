# Copyright 2020 The Chromium OS Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/pkg_config.gni")

# Library that provides core functionality for the wilco_dtc_supportd daemon.
pkg_config("libwilco_dtc_supportd_pkg_deps") {
  pkg_deps = [
    "dbus-1",
    "libbrillo",
    "libchrome-${libbase_ver}",
    "libdebugd-client",
    "libmojo-${libbase_ver}",
    "system_api",
  ]
}

source_set("libwilco_dtc_supportd") {
  configs += [ ":libwilco_dtc_supportd_pkg_deps" ]
  deps = [
    "//diagnostics/common:libcommon",
    "//diagnostics/constants:libconstants",
    "//diagnostics/grpc:wilco_dtc_grpc_protos",
    "//diagnostics/grpc_async_adapter:libgrpc_async_adapter",
    "//diagnostics/mojo:diagnostics_mojo_bindings",
    "//diagnostics/routines:libdiag_routine",
  ]
  sources = [
    "core.cc",
    "core_delegate_impl.cc",
    "daemon.cc",
    "dbus_service.cc",
    "ec_constants.cc",
    "grpc_service.cc",
    "json_utils.cc",
    "mojo_service.cc",
    "probe_service_impl.cc",
    "routine_service.cc",
    "telemetry/bluetooth_event_service.cc",
    "telemetry/bluetooth_event_service_impl.cc",
    "telemetry/ec_event_service.cc",
    "telemetry/powerd_event_service_impl.cc",
    "telemetry/system_files_service.cc",
    "telemetry/system_files_service_impl.cc",
    "telemetry/system_info_service_impl.cc",
  ]
}

executable("wilco_dtc_supportd") {
  deps = [ ":libwilco_dtc_supportd" ]
  sources = [ "main.cc" ]
}

if (use.test) {
  pkg_config("wilco_dtc_supportd_test_pkg_deps") {
    pkg_deps = [
      "libbrillo",
      "libchrome-${libbase_ver}",
      "libchrome-test-${libbase_ver}",
    ]
  }

  executable("wilco_dtc_supportd_test") {
    configs += [
      ":wilco_dtc_supportd_test_pkg_deps",
      "//common-mk:test",
    ]
    deps = [
      ":libwilco_dtc_supportd",
      "//common-mk/testrunner:testrunner",
      "//diagnostics/common:libcommon",
      "//diagnostics/common:libcommon_test_utils",
    ]
    sources = [
      "core_test.cc",
      "dbus_service_test.cc",
      "ec_constants_test.cc",
      "fake_browser.cc",
      "fake_diagnostics_service.cc",
      "fake_probe_service.cc",
      "fake_wilco_dtc.cc",
      "grpc_service_test.cc",
      "mock_mojo_client.cc",
      "mojo_service_test.cc",
      "probe_service_impl_test.cc",
      "routine_service_test.cc",
      "telemetry/bluetooth_event_service_impl_test.cc",
      "telemetry/ec_event_service_test.cc",
      "telemetry/ec_event_test_utils.cc",
      "telemetry/ec_event_test_utils_test.cc",
      "telemetry/fake_bluetooth_event_service.cc",
      "telemetry/fake_ec_event_service.cc",
      "telemetry/fake_powerd_event_service.cc",
      "telemetry/mock_system_files_service.cc",
      "telemetry/mock_system_info_service.cc",
      "telemetry/powerd_event_service_impl_test.cc",
      "telemetry/system_files_service_impl_test.cc",
      "telemetry/system_info_service_impl_test.cc",
    ]
  }
}
