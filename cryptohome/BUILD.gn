# Copyright 2018 The Chromium OS Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//common-mk/pkg_config.gni")
import("//common-mk/proto_library.gni")

group("all") {
  deps = [
    ":cryptohome",
    ":cryptohome-path",
    ":cryptohomed",
    ":lockbox-cache",
    ":mount-encrypted",
    ":mount_encrypted_lib",
    ":tpm-manager",
  ]
  if (use.cert_provision) {
    deps += [
      ":cert_provision",
      ":cert_provision-proto",
      ":cert_provision-static",
      ":cert_provision_client",
    ]
  }
  if (use.test) {
    deps += [
      ":boot_lockbox_unittests",
      ":cryptohome_testrunner",
      ":mount_encrypted_unittests",
    ]
  }
  if (use.fuzzer) {
    deps += [ ":cryptohome_cryptolib_rsa_oaep_decrypt_fuzzer" ]
  }
  if (use.tpm2) {
    deps += [
      ":bootlockboxd",
      ":bootlockboxtool",
    ]
  }
}

pkg_config("cryptohome_config") {
  pkg_deps = [
    "dbus-1",
    "dbus-glib-1",
    "glib-2.0",
    "libbrillo-${libbase_ver}",
    "libbrillo-glib-${libbase_ver}",
    "libchrome-${libbase_ver}",
    "libecryptfs",
    "libmetrics-${libbase_ver}",
    "openssl",
    "protobuf",
    "vboot_host",
  ]
}

# Main programs.
executable("cryptohome") {
  configs += [
    ":cryptohome_config",
    "libs:target_defaults",
  ]
  sources = [
    "cryptohome.cc",
  ]
  libs = [
    "-lchaps",
    "-lkeyutils",
    "-lpolicy-${libbase_ver}",
    "-lpthread",
    "-lscrypt",
  ]
  deps = [
    "libs:cryptohome-dbus-client",
    "libs:cryptohome-proto",
    "libs:cryptohome-proto-external",
    "libs:libcrostpm",
    "libs:libcryptohome",
  ]
}

pkg_config("cryptohome-path_config") {
  pkg_deps = [
    "libbrillo-${libbase_ver}",
    "libchrome-${libbase_ver}",
  ]
}

executable("cryptohome-path") {
  configs += [ ":cryptohome-path_config" ]
  sources = [
    "cryptohome-path.cc",
  ]
  deps = [
    "libs:libcrostpm",
    "libs:libcryptohome",
  ]
}

pkg_config("cryptohomed_config") {
  pkg_deps = [
    "dbus-1",
    "dbus-glib-1",
    "glib-2.0",
    "libbrillo-${libbase_ver}",
    "libbrillo-glib-${libbase_ver}",
    "libchrome-${libbase_ver}",
    "libecryptfs",
    "libmetrics-${libbase_ver}",
    "openssl",

    # system_api depends on protobuf (or protobuf-lite). It must appear
    # before protobuf here or the linker flags won't be in the right
    # order.
    "system_api",
    "protobuf",
    "vboot_host",
  ]
}

executable("cryptohomed") {
  configs += [
    ":cryptohomed_config",
    "libs:target_defaults",
  ]
  sources = [
    "cryptohomed.cc",
  ]
  libs = [
    "-lchaps",
    "-lkeyutils",
    "-lpolicy-${libbase_ver}",
    "-lpthread",
    "-lscrypt",
  ]
  deps = [
    "libs:libcrostpm",
    "libs:libcryptohome",
  ]
}

pkg_config("lockbox-cache_config") {
  pkg_deps = [
    "libbrillo-${libbase_ver}",
    "libchrome-${libbase_ver}",
    "libecryptfs",
    "libmetrics-${libbase_ver}",
    "openssl",
    "vboot_host",
  ]
}

executable("lockbox-cache") {
  configs += [
    ":lockbox-cache_config",
    "libs:target_defaults",
  ]
  sources = [
    "crc32.c",
    "dircrypto_util.cc",
    "lockbox-cache-main.cc",
    "lockbox-cache-tpm.cc",
    "lockbox-cache.cc",
    "lockbox.cc",
    "platform.cc",
  ]
  libs = [
    "-lkeyutils",
    "-lsecure_erase_file",
  ]
  deps = [
    "libs:cryptohome-proto",
    "libs:libcrostpm",
  ]
}

pkg_config("mount_encrypted_lib_config") {
  pkg_deps = [
    "glib-2.0",
    "libbrillo-${libbase_ver}",
    "libchrome-${libbase_ver}",
    "openssl",
    "vboot_host",
  ]
}

static_library("mount_encrypted_lib") {
  configs += [
    ":mount_encrypted_lib_config",

    # for USE_TPM2
    "libs:target_defaults",
  ]
  sources = [
    "mount_encrypted/encryption_key.cc",
    "mount_encrypted/tpm.cc",
    "mount_helpers.cc",
  ]
  defines = [ "CHROMEOS_ENVIRONMENT=1" ]
  deps = [
    "libs:cryptohome-proto",
  ]

  if (use.tpm2) {
    sources += [ "mount_encrypted/tpm2.cc" ]

    # This selects TPM2 code in vboot_host headers.
    defines += [ "TPM2_MODE=1" ]
  }

  if (!use.tpm2) {
    sources += [ "mount_encrypted/tpm1.cc" ]
  }
}

executable("mount-encrypted") {
  configs += [ ":mount_encrypted_lib_config" ]
  sources = [
    "mount_encrypted.cc",
  ]
  deps = [
    ":mount_encrypted_lib",
    "libs:libcrostpm",
  ]
}

pkg_config("tpm-manager_config") {
  pkg_deps = [
    "glib-2.0",
    "libbrillo-${libbase_ver}",
    "libbrillo-glib-${libbase_ver}",
    "libchrome-${libbase_ver}",
    "libecryptfs",
    "libmetrics-${libbase_ver}",
    "openssl",
    "protobuf",
  ]
  if (!use.tpm2) {
    pkg_deps += [ "vboot_host" ]
  }
}

executable("tpm-manager") {
  configs += [
    ":tpm-manager_config",
    "libs:target_defaults",
  ]
  sources = [
    "tpm_manager.cc",
  ]
  deps = [
    "libs:libcrostpm",
  ]

  if (use.tpm2) {
    sources += [ "tpm_manager_v2.cc" ]
    libs = [
      "-ltrunks",
      "-ltpm_manager",
      "-lattestation",
    ]
  }

  if (!use.tpm2) {
    sources += [ "tpm_manager_v1.cc" ]
    libs = [
      "-lchaps",
      "-lscrypt",
    ]
  }
}

if (use.cert_provision) {
  pkg_config("cert_provision-proto_config") {
    pkg_deps = [ "protobuf" ]
  }

  proto_library("cert_provision-proto") {
    proto_in_dir = "./cert"
    proto_out_dir = "include/cert"
    use_pic = true
    sources = [
      "cert/cert_provision.proto",
    ]

    # libcert_provision-proto.a is used by a shared_libary
    all_dependent_configs = [ ":cert_provision-proto_config" ]
  }

  shared_library("cert_provision") {
    deps = [
      ":cert_provision-static",
    ]
  }

  pkg_config("cert_provision-static_direct_config") {
    pkg_deps = [
      "dbus-glib-1",
      "libbrillo-${libbase_ver}",
      "libbrillo-glib-${libbase_ver}",
      "libchrome-${libbase_ver}",
      "openssl",
      "protobuf",
    ]
  }

  static_library("cert_provision-static") {
    configs += [ ":cert_provision-proto_config" ]
    sources = [
      "cert/cert_provision.cc",
      "cert/cert_provision_cryptohome.cc",
      "cert/cert_provision_keystore.cc",
      "cert/cert_provision_pca.cc",
      "cert/cert_provision_util.cc",
    ]

    # libcert_provision-static.a is used by a shared_libary
    # object, so we need to build it with '-fPIC' instead of '-fPIE'.
    configs -= [ "//common-mk:pie" ]
    configs += [ "//common-mk:pic" ]
    libs = [
      "-lchaps",
      "-lpthread",
    ]
    public_configs = [ ":cert_provision-static_direct_config" ]

    deps = [
      ":cert_provision-proto",
      "libs:cryptohome-dbus-client",
      "libs:cryptohome-proto",
    ]
  }

  executable("cert_provision_client") {
    configs += [ ":cryptohome-path_config" ]
    sources = [
      "cert/cert_provision_client.cc",
    ]
    deps = [
      ":cert_provision",
    ]
  }
}

if (use.test) {
  pkg_config("cryptohome_testrunner_config") {
    pkg_deps = [
      "dbus-1",
      "dbus-glib-1",
      "glib-2.0",
      "libbrillo-${libbase_ver}",
      "libbrillo-glib-${libbase_ver}",
      "libbrillo-test-${libbase_ver}",
      "libchrome-${libbase_ver}",
      "libchrome-test-${libbase_ver}",
      "libecryptfs",
      "libmetrics-${libbase_ver}",
      "openssl",
      "protobuf",
      "vboot_host",
    ]
  }

  executable("cryptohome_testrunner") {
    configs += [
      "//common-mk:test",
      "libs:target_defaults",
      ":cryptohome_testrunner_config",
    ]
    sources = [
      "arc_disk_quota_unittest.cc",
      "attestation_unittest.cc",
      "boot_attributes_unittest.cc",
      "bootlockbox/boot_lockbox_unittest.cc",
      "challenge_credentials/challenge_credentials_decrypt_operation_unittest.cc",
      "challenge_credentials/challenge_credentials_test_utils.cc",
      "crypto_unittest.cc",
      "cryptohome_event_source_unittest.cc",
      "cryptolib_unittest.cc",
      "dircrypto_data_migrator/migration_helper_unittest.cc",
      "fake_le_credential_backend.cc",
      "firmware_management_parameters_unittest.cc",
      "homedirs_unittest.cc",
      "install_attributes_unittest.cc",
      "le_credential_manager_unittest.cc",
      "lockbox-cache-unittest.cc",
      "lockbox_unittest.cc",
      "make_tests.cc",
      "mock_chaps_client_factory.cc",
      "mock_firmware_management_parameters.cc",
      "mock_homedirs.cc",
      "mock_install_attributes.cc",
      "mock_key_challenge_service.cc",
      "mock_keystore.cc",
      "mock_le_credential_manager.cc",
      "mock_lockbox.cc",
      "mock_mount.cc",
      "mock_pkcs11_init.cc",
      "mock_platform.cc",
      "mock_service.cc",
      "mock_signature_sealing_backend.cc",
      "mock_tpm.cc",
      "mock_tpm_init.cc",
      "mock_user_oldest_activity_timestamp_cache.cc",
      "mock_user_session.cc",
      "mock_vault_keyset.cc",
      "mount_stack_unittest.cc",
      "mount_task_unittest.cc",
      "mount_unittest.cc",
      "obfuscated_username_unittest.cc",
      "persistent_lookup_table_unittest.cc",
      "pkcs11_keystore_unittest.cc",
      "platform_unittest.cc",
      "service_unittest.cc",
      "sign_in_hash_tree_unittest.cc",
      "signature_sealing_backend_test_utils.cc",
      "stateful_recovery_unittest.cc",
      "tpm_init_unittest.cc",
      "tpm_persistent_state_unittest.cc",
      "user_oldest_activity_timestamp_cache_unittest.cc",
      "user_session_unittest.cc",
      "username_passkey_unittest.cc",
      "vault_keyset_unittest.cc",
    ]
    libs = [
      "-lchaps",
      "-lkeyutils",
      "-lpolicy-${libbase_ver}",
      "-lpthread",
      "-lscrypt",
    ]
    deps = [
      "libs:libcrostpm",
      "libs:libcryptohome",
      "libs:cryptohome-proto",
      "//common-mk/testrunner",
    ]

    if (use.tpm2) {
      sources += [ "tpm2_test.cc" ]
      libs += [
        "-ltrunks_test",
        "-ltpm_manager_test",
      ]
    }

    if (use.cert_provision) {
      sources += [
        "cert/cert_provision_keystore_unittest.cc",
        "cert/cert_provision_unittest.cc",
      ]
      deps += [ ":cert_provision-static" ]
    }
  }

  pkg_config("mount_encrypted_unittests_config") {
    pkg_deps = [
      "glib-2.0",
      "libbrillo-${libbase_ver}",
      "libbrillo-test-${libbase_ver}",
      "libchrome-${libbase_ver}",
      "libchrome-test-${libbase_ver}",
    ]
  }

  executable("mount_encrypted_unittests") {
    configs += [
      "//common-mk:test",
      "libs:target_defaults",
      ":mount_encrypted_unittests_config",
    ]
    sources = [
      "mount_encrypted/encryption_key_unittest.cc",
      "mount_encrypted/tlcl_stub.cc",
    ]
    deps = [
      ":mount_encrypted_lib",
      "libs:libcrostpm",
      "//common-mk/testrunner",
    ]

    if (use.tpm2) {
      defines = [ "TPM2_MODE=1" ]
    }
  }

  pkg_config("boot_lockbox_unittests_config") {
    pkg_deps = [
      "glib-2.0",
      "libbrillo-${libbase_ver}",
      "libbrillo-test-${libbase_ver}",
      "libchrome-${libbase_ver}",
      "libchrome-test-${libbase_ver}",
      "openssl",
    ]
  }

  executable("boot_lockbox_unittests") {
    configs += [
      "//common-mk:test",
      "libs:target_defaults",
      ":boot_lockbox_unittests_config",
    ]
    deps = [
      "//common-mk/testrunner",
    ]

    if (use.tpm2) {
      sources = [
        "bootlockbox/boot_lockbox_dbus_adaptor.cc",
        "bootlockbox/boot_lockbox_service_unittest.cc",
        "bootlockbox/fake_tpm_nvspace_utility.cc",
        "bootlockbox/nvram_boot_lockbox_unittest.cc",
        "bootlockbox/tpm2_nvspace_utility_unittest.cc",
        "mock_tpm_init.cc",
      ]
      defines = [ "TPM2_MODE=1" ]
      libs = [
        "-ltpm_manager",
        "-ltpm_manager_test",
        "-ltrunks",
        "-ltrunks_test",
      ]
      deps += [
        ":tpm-manager",
        "libs:bootlockbox-proto",
        "libs:libcrostpm",
        "libs:libnvram-boot-lockbox",
      ]
    }
  }
}

if (use.tpm2) {
  pkg_config("bootlockboxtool_config") {
    pkg_deps = [
      "libchrome-${libbase_ver}",
      "libbrillo-${libbase_ver}",
      "protobuf",
    ]
  }

  executable("bootlockboxtool") {
    configs += [ ":bootlockboxtool_config" ]
    defines = [ "USE_TPM2=${use.tpm2}" ]
    deps = [
      "libs:bootlockbox-client-headers",
      "libs:bootlockbox-proto",
      "libs:libnvram-boot-lockbox",
      "libs:libnvram-boot-lockbox-client",
    ]
    sources = [
      "bootlockbox/boot_lockbox_tool.cc",
    ]
  }

  pkg_config("bootlockboxd_config") {
    pkg_deps = [
      "libbrillo-${libbase_ver}",
      "libchrome-${libbase_ver}",
      "libmetrics-${libbase_ver}",
      "libecryptfs",
      "openssl",
      "protobuf",
      "vboot_host",
    ]
  }

  executable("bootlockboxd") {
    configs += [ ":bootlockboxd_config" ]
    deps = [
      "libs:libcrostpm",
      "libs:bootlockbox-adaptors",
      "libs:bootlockbox-proto",
      "libs:libnvram-boot-lockbox",
    ]
    defines = [ "USE_TPM2=${use.tpm2}" ]
    libs = [
      "-lscrypt",
      "-lchaps",
      "-lkeyutils",
      "-ltpm_manager",
      "-ltrunks",
    ]
    sources = [
      "bootlockbox/boot_lockbox_dbus_adaptor.cc",
      "bootlockbox/boot_lockbox_service.cc",
      "bootlockbox/boot_lockboxd.cc",
    ]
  }
}

if (use.fuzzer) {
  pkg_config("cryptohome_cryptolib_rsa_oaep_decrypt_fuzzer_config") {
    pkg_deps = [
      "libbrillo-${libbase_ver}",
      "libchrome-${libbase_ver}",
      "openssl",
    ]
  }

  executable("cryptohome_cryptolib_rsa_oaep_decrypt_fuzzer") {
    configs += [
      "//common-mk/common_fuzzer",
      ":cryptohome_cryptolib_rsa_oaep_decrypt_fuzzer_config",
    ]
    sources = [
      "fuzzers/cryptolib_rsa_oaep_decrypt_fuzzer.cc",
    ]
    deps = [
      "libs:libcrosplatform",
    ]
  }
}
